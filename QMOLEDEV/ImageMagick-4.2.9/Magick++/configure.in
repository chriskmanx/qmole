dnl Process this file with autoconf to produce a configure script.
dnl Written by Bob Friesenhahn, bfriesen@simple.dallas.tx.us
#
# Magick++ Configure Script
#
# Report Magick++ configure bugs to bfriesen@simple.dallas.tx.us
#


AC_INIT(lib/Magick++.h)

MAGICK_RELEASE="0.8.6"		# Magick++ release
TESTED_MAGICK_VERSION="4.2.9"	# Last tested ImageMagick release

dnl The most recent interface number that this library implements.
INTERFACE_CURRENT=0
dnl The implementation number of the current interface. 
INTERFACE_REVISION=0
dnl The difference between the newest and oldest interfaces that this
dnl library implements. In other words, the library implements all the
dnl interface numbers in the range from number current - age to current.
INTERFACE_AGE=0

echo "configuring Magick++ $MAGICK_RELEASE"

# Check for ImageMagick release and warn if not what we expect
AC_MSG_CHECKING(ImageMagick version)
MAGICK_VERSION=`Magick-config --version`
AC_MSG_RESULT("version $MAGICK_VERSION")
if test "$MAGICK_VERSION" != "$TESTED_MAGICK_VERSION"
then
  AC_MSG_WARN("ImageMagick version is not expected version $TESTED_MAGICK_VERSION !!!")
fi

AC_CANONICAL_SYSTEM
AM_INIT_AUTOMAKE(Magick++, $MAGICK_RELEASE, ' ')

AC_SUBST(INTERFACE_CURRENT)dnl
AC_SUBST(INTERFACE_REVISION)dnl
AC_SUBST(INTERFACE_AGE)dnl


# Ensure that make can run correctly
AM_SANITY_CHECK

# Override configure's default compilation flags
CFLAGS="$CFLAGS"
CXXFLAGS="$CXXFLAGS"

# Check for programs
AC_PROG_CC
AC_PROG_CPP
AM_PROG_CC_STDC
AC_PROG_CXX
AC_LANG_CPLUSPLUS
AC_PROG_INSTALL
AC_PROG_LN_S
AC_PROG_RANLIB

# Configure libtool
AM_ENABLE_SHARED(no)
AM_ENABLE_STATIC(yes)
AM_PROG_LIBTOOL

# Add configure option --enable-maintainer-mode which enables dependency
# checking and generation useful to package maintainers.  This is made an
# option to avoid confusing end users.
AM_MAINTAINER_MODE
AC_CANONICAL_HOST

#
# Check for using dmalloc malloc debugging library
#
AM_WITH_DMALLOC

#
# Obtain ImageMagick compilation options from installed Magick-config script.
#

AC_MSG_CHECKING(pre-processor flags)
# If -DHAVE_CONFIG_H is not specified, then QuantumLeap doesn't work.
CPPFLAGS="$CPPFLAGS -DHAVE_CONFIG_H "`Magick-config --cppflags`
AC_MSG_RESULT("$CPPFLAGS")

AC_MSG_CHECKING(linker flags)
LDFLAGS="$LDFLAGS "`Magick-config --ldflags`
AC_MSG_RESULT("$LDFLAGS")

AC_MSG_CHECKING(libraries)
LIBS="$LIBS "`Magick-config --libs`
AC_MSG_RESULT("$LIBS")

AC_OUTPUT(Makefile lib/Makefile lib/Magick++/Makefile demo/Makefile tests/Makefile)
