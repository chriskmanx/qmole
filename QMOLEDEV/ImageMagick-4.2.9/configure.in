dnl Process this file with autoconf to produce a configure script.
dnl Currently maintained by Bob Friesenhahn, bfriesen@simple.dallas.tx.us
#
# ImageMagick Configure Script
#
# Report ImageMagick configure bugs to bfriesen@simple.dallas.tx.us
#

AC_INIT(magick/magick.h)

# Set version
MAJOR_VERSION=4
MINOR_VERSION=2
MICRO_VERSION=9
echo "configuring ImageMagick $MAJOR_VERSION.$MINOR_VERSION.$MICRO_VERSION"
AC_CANONICAL_SYSTEM
AC_SUBST(MAJOR_VERSION)dnl
AC_SUBST(MINOR_VERSION)dnl
AC_SUBST(MICRO_VERSION)dnl

# Generate configure header.
AM_CONFIG_HEADER(magick/config.h)

AM_INIT_AUTOMAKE(ImageMagick,$MAJOR_VERSION.$MINOR_VERSION.$MICRO_VERSION, ' ')

# Set default CFLAGS (if not already set)
if test "${CFLAGS}x" = 'x';
then
  CFLAGS='-O'
fi

# Check for programs
AC_PROG_CC
AC_PROG_CPP
AM_PROG_CC_STDC
AC_PROG_INSTALL
AC_PROG_MAKE_SET
AC_PROG_LN_S

# Tests for Windows
AC_CYGWIN
AC_EXEEXT
AC_OBJEXT
AC_MINGW32
# Declare that the library is prepared to build a DLL
# Must also pass -no-undefined to libtool in Makefile
AC_LIBTOOL_WIN32_DLL

# Configure libtool
AM_ENABLE_SHARED(no)
AM_ENABLE_STATIC(yes)
AM_PROG_LIBTOOL

# Check to see if building shared libraries
libtool_build_shared_libs='no'
if ./libtool --config | grep 'build_libtool_libs=yes' > /dev/null
then
  libtool_build_shared_libs='yes'
fi

# Check to see if building static libraries
libtool_build_static_libs='no'
if ./libtool --config | grep 'build_old_libs=yes' > /dev/null
then
  libtool_build_static_libs='yes'
fi


# Add configure option --enable-maintainer-mode which enables dependency
# checking and generation useful to package maintainers.  This is made an
# option to avoid confusing end users.
AM_MAINTAINER_MODE
AC_CANONICAL_HOST

# Add configure option --with-dmalloc to enable malloc debugging.
# dmalloc is available at ftp://ftp.letters.com/src/dmalloc/dmalloc.tar.gz
AM_WITH_DMALLOC

# Enable prof-based profiling support
AC_ARG_ENABLE(prof,
	      [  --enable-prof           enable prof source profiling support (default is no)],
	      [with_prof=$enableval],
	      [with_prof='no'])

# Enable gprof-based profiling support
AC_ARG_ENABLE(gprof,
	      [  --enable-gprof          enable gprof source profiling support (default is no)],
	      [with_gprof=$enableval],
	      [with_gprof='no'])

# Enable gcov-based profiling support
AC_ARG_ENABLE(gcov,
	      [  --enable-gcov           enable gcov source profiling support (default is no)],
	      [with_gcov=$enableval],
	      [with_gcov='no'])

with_profiling='no'
if test "$with_prof" = 'yes' || test "$with_gprof" = 'yes' || test "$with_gcov" = 'yes'
then
  with_profiling='yes'

  if test "$libtool_build_shared_libs" = 'yes'
  then
    echo "Warning: Can not profile code using shared libraries"
  fi
fi


# Enable LZW compression.
AC_ARG_ENABLE(lzw,
	      [  --enable-lzw            enable LZW support (default is no)],
	      [with_lzw=$enableval],
	      [with_lzw='no'])
test "$with_lzw" = 'yes' && AC_DEFINE(HasLZW)

# Enable 16-bit pixels.
AC_ARG_ENABLE(16bit-pixel,
	      [  --enable-16bit-pixel    enable 16 bit pixels (default is no)],
	      [with_16_bit_pixel=$enableval],
	      [with_16_bit_pixel='no'])
test "$with_16_bit_pixel" = 'yes' && AC_DEFINE(QuantumLeap)

# Enable sfio stdio support
AC_ARG_ENABLE(sfio,
	      [  --enable-sfio           enable sfio-based stdio support (default is no)],
	      [with_sfio=$enableval],
	      [with_sfio='no'])

# Enable SOCKS version 5 proxy support
AC_ARG_ENABLE(socks,
	      [  --enable-socks          enable SOCKS v5 proxy support (default is no)],
	      [with_socks=$enableval],
	      [with_socks='no'])

# Enable support for full delegate paths
AC_ARG_WITH(frozenpaths,
	      [  --with-frozenpaths      enable frozen delegate paths (default is yes)],
	      [with_frozenpaths=$withval],
	      [with_frozenpaths='yes'])

# Enable build/install of PerlMagick.
AC_ARG_WITH(perl,
	      [  --with-perl             enable build/install of PerlMagick (default is yes)],
	      [with_perl=$withval],
	      [with_perl='yes'])

AM_CONDITIONAL(WITH_PERL, test "$with_perl" != 'no')
with_perl_static='no'
if test "$with_perl" != 'no' && test "$libtool_build_shared_libs" = 'no'
then
  with_perl_static='yes'
fi
with_perl_dynamic='no'
if test "$with_perl" != 'no' && test "$libtool_build_shared_libs" = 'yes'
then
  with_perl_dynamic='yes'
fi
AM_CONDITIONAL(WITH_PERL_STATIC, test $with_perl_static = 'yes')
AM_CONDITIONAL(WITH_PERL_DYNAMIC, test $with_perl_dynamic = 'yes')

# Enable BZLIB (bzip2 library)
AC_ARG_WITH(bzlib,
	      [  --with-bzlib            enable BZLIB (default is yes)],
	      [with_bzlib=$withval],
	      [with_bzlib='yes'])

# Enable Display Postscript.
AC_ARG_WITH(dps,
	      [  --with-dps              enable Display Postscript (default is yes)],
	      [with_dps=$withval],
	      [with_dps='yes'])

# Enable FlashPIX.
AC_ARG_WITH(fpx,
	      [  --with-fpx              enable FlashPIX (default is yes)],
	      [with_fpx=$withval],
	      [with_fpx='yes'])

# Enable HDF.
AC_ARG_WITH(hdf,
	      [  --with-hdf              enable HDF (default is yes)],
	      [with_hdf=$withval],
	      [with_hdf='yes'])

# Enable JBIG.
AC_ARG_WITH(jbig,
	      [  --with-jbig             enable JBIG (default is yes)],
	      [with_jbig=$withval],
	      [with_jbig='yes'])

# Enable JPEG.
AC_ARG_WITH(jpeg,
	      [  --with-jpeg             enable JPEG (default is yes)],
	      [with_jpeg=$withval],
	      [with_jpeg='yes'])

# Enable PNG.
AC_ARG_WITH(png,
	      [  --with-png              enable PNG (default is yes)],
	      [with_png=$withval],
	      [with_png='yes'])

# Enable TIFF.
AC_ARG_WITH(tiff,
	      [  --with-tiff             enable TIFF (default is yes)],
	      [with_tiff=$withval],
	      [with_tiff='yes'])

# Enable TTF.
AC_ARG_WITH(ttf,
	      [  --with-ttf              enable TrueType (default is yes)],
	      [with_ttf=$withval],
	      [with_ttf='yes'])

# Set default TrueType font path
AC_ARG_WITH(ttf-fontpath,
	      [  --with-ttf-fontpath    set default TrueType font path (default none)],
	      [with_ttf_fontpath=$withval],
	      [with_ttf_fontpath=''])
if test "$with_ttf_fontpath" != "yes" && test -z "$with_ttf_fontpath"
then
  with_ttf_fontpath=''
else
  AC_DEFINE_UNQUOTED(TT_FONT_PATH,"$with_ttf_fontpath")
fi

AC_ARG_WITH(zlib,
	      [  --with-zlib             enable ZLIB (default is yes)],
	      [with_zlib=$withval],
	      [with_zlib='yes'])

# Check standard headers
AC_HEADER_STDC
AC_HEADER_STAT
AC_HEADER_TIME
AC_HEADER_SYS_WAIT
AC_HEADER_DIRENT
AC_CHECK_HEADER(elf.h,AC_DEFINE(SVR4))
AC_CHECK_HEADERS(sys/time.h sys/stat.h sys/types.h)
AC_CHECK_HEADERS(math.h string.h stdarg.h pwd.h malloc.h)
AC_CHECK_HEADERS(memory.h errno.h fcntl.h)

# Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST

AC_RETSIGTYPE
AC_TYPE_MODE_T
AC_TYPE_OFF_T
AC_TYPE_PID_T
AC_TYPE_SIZE_T


# Checks for library functions.
AC_PROG_GCC_TRADITIONAL
AC_FUNC_MEMCMP
AC_TYPE_SIGNAL
AC_FUNC_VPRINTF
AC_FUNC_WAIT3
AC_CHECK_FUNCS(strchr strtol getcwd mkdir select tempnam)
AC_FUNC_MMAP

# Check standard functions
AC_CHECK_FUNCS(strerror)
AC_CHECK_FUNCS(snprintf)
AC_CHECK_FUNCS(vsnprintf)
AC_MSG_CHECKING(for sys_errlist)
AC_TRY_LINK([extern char** sys_errlist;],[char** p = sys_errlist;],
AC_MSG_RESULT(yes); AC_DEFINE(HAVE_SYS_ERRLIST),AC_MSG_RESULT(no))


# Check for delegate sub-directories and add -I & -L options as required.
# This presumes that delegates are installed as detailed in the ImageMagick
# README. If delegates are installed in a standard location where the
# compiler will automatically find them then these options should not be
# required.

srcdirfull=`cd $srcdir && pwd`
builddir=`pwd`

#
# Sfio has a different structure than most delegates
#
if test "$with_sfio" != 'no'
then
  if test -d "$builddir/sfio/include" && test -d "$builddir/sfio/lib"
  then
    CPPFLAGS="$CPPFLAGS -I$builddir/sfio/include"
    LDFLAGS="$LDFLAGS -L$builddir/sfio/lib"
  else
    if test -d "$srcdirfull/sfio/include" && test -d "$srcdirfull/sfio/lib"
    then
      CPPFLAGS="$CPPFLAGS -I$srcdirfull/sfio/include"
      LDFLAGS="$LDFLAGS -L$srcdirfull/sfio/lib"
    fi
  fi
fi

#
# Most delegates have includes in the same directory as the library
#
for dir in bzlib hdf/hdf/src jbig/libjbig jpeg zlib png tiff/libtiff ttf ttf/lib ttf/lib/.libs
do
  if test -d "$builddir/$dir"
  then
    CPPFLAGS="$CPPFLAGS -I$builddir/$dir"
    LDFLAGS="$LDFLAGS -L$builddir/$dir"
  else
    if test -d "$srcdirfull/$dir"
    then
      CPPFLAGS="$CPPFLAGS -I$srcdirfull/$dir"
      LDFLAGS="$LDFLAGS -L$srcdirfull/$dir"
    fi
  fi
done

#
# The following is extracted from AC_OUTPUT since we need the values of
# prefix and exec_prefix earlier.
# Reportedly fixed in autoconf 1.13 by the addition of AC_DEFINE_DIR
#
test "x$prefix" = xNONE && prefix=$ac_default_prefix
# Let make expand exec_prefix.
test "x$exec_prefix" = xNONE && exec_prefix='${prefix}'

# Evaluate shell variable equivalents to Makefile directory variables
eval "eval BIN_DIR=$bindir"
eval "eval SBIN_DIR=$sbindir"
eval "eval LIBEXEC_DIR=$libexecdir"
eval "eval DATA_DIR=$datadir"
eval "eval SYSCONF_DIR=$sysconfdir"
eval "eval SHAREDSTATE_DIR=$sharedstatedir"
eval "eval LOCALSTATE_DIR=$localstatedir"
eval "eval LIB_DIR=$libdir"
eval "eval INCLUDE_DIR=$includedir"
eval "eval OLDINCLUDE_DIR=$oldincludedir"
eval "eval INFO_DIR=$infodir"
eval "eval MAN_DIR=$mandir"


LDFLAGS="$LDFLAGS -L$LIB_DIR"
CPPFLAGS="$CPPFLAGS -I$INCLUDE_DIR"

# Extend includedir to include magick subdirectory offset
includedir="$includedir/magick"



#
# Find the X11 RGB database
#
AC_CACHE_CHECK(for X11 RGB database,im_cv_x_rgbdb,
[# Look for the header file in a standard set of common directories.
# Check X11 before X11Rn because it is often a symlink to the current release.
  for ac_dir in               \
    /lib/usr/lib/X11	  \
    /usr/X11/lib          \
    /usr/X11R4/lib        \
    /usr/X11R5/lib        \
    /usr/X11R6/lib        \
    /usr/X386/lib         \
    /usr/XFree86/lib/X11  \
    /usr/athena/lib       \
    /usr/lib              \
    /usr/lib/X11          \
    /usr/lib/X11R4        \
    /usr/lib/X11R5        \
    /usr/lib/X11R6        \
    /usr/local/X11/lib    \
    /usr/local/X11R4/lib  \
    /usr/local/X11R5/lib  \
    /usr/local/X11R6/lib  \
    /usr/local/lib        \
    /usr/local/lib/X11    \
    /usr/local/lib/X11R4  \
    /usr/local/lib/X11R5  \
    /usr/local/lib/X11R6  \
    /usr/local/x11r5/lib  \
    /usr/lpp/Xamples/lib  \
    /usr/openwin/lib      \
    /usr/openwin/share/lib \
    /usr/unsupported/lib  \
    /usr/x386/lib         \
    ; \
  do
    if test -f "$ac_dir/X11/rgb.txt"
    then
        im_cv_x_rgbdb="$ac_dir/X11/rgb.txt"
	break
    elif test -f "$ac_dir/rgb.txt"
    then 
        im_cv_x_rgbdb="$ac_dir/rgb.txt"
        break
    fi

  done])
AC_DEFINE_UNQUOTED(RGBColorDatabase,"$im_cv_x_rgbdb")

#
# Save initial user-tunable values
#
CC_INITIAL=$CC
CFLAGS_INITIAL=$CFLAGS
CPPFLAGS_INITIAL=$CPPFLAGS
LDFLAGS_INITIAL=$LDFLAGS
LIBS_INITIAL=$LIBS

#
# Find math library
#
LIB_MATH=''
AC_CHECK_LIB(m,sqrt,LIB_MATH="-lm",,)
LIBS="$LIB_MATH $LIBS"

#
# Check for Sfio
#
have_sfio='no'
LIB_SFIO=''
if test "$with_sfio" != 'no'
then
    AC_CHECKING(for SFIO support ...)
    failed=0;
    passed=0;
    AC_CHECK_HEADER(stdio.h,passed=`expr $passed + 1`,failed=`expr $failed + 1`)
    AC_CHECK_LIB(sfio,_stdopen,passed=`expr $passed + 1`,failed=`expr $failed + 1`,)
    AC_CHECK_LIB(stdio,vprintf,passed=`expr $passed + 1`,failed=`expr $failed + 1`,-lsfio)
    AC_MSG_CHECKING(if SFIO package is complete)
    if test $passed -gt 0
    then
	if test $failed -gt 0
	then
	    AC_MSG_RESULT(no -- some components failed test)
	    have_sfio='no (failed tests)'
	else
	    LIB_SFIO='-lstdio -lsfio'
	    LIBS="$LIB_SFIO $LIBS"
	    AC_DEFINE(HasSFIO)
	    AC_MSG_RESULT(yes)
	    have_sfio='yes'
	fi
    else
	AC_MSG_RESULT(no)
    fi
fi


#
# Check for ZLIB
#
have_zlib='no'
dnl PNG requires zlib so enable zlib check if PNG is requested
if test "$with_zlib" != 'no' || test "$with_png" != 'no'
then
  LIB_ZLIB=''
  AC_CHECKING(for ZLIB support ...)
  failed=0;
  passed=0;
  AC_CHECK_HEADER(zconf.h,passed=`expr $passed + 1`,failed=`expr $failed + 1`)
  AC_CHECK_HEADER(zlib.h,passed=`expr $passed + 1`,failed=`expr $failed + 1`)
  AC_CHECK_LIB(z,compress,passed=`expr $passed + 1`,failed=`expr $failed + 1`,)
  AC_CHECK_LIB(z,uncompress,passed=`expr $passed + 1`,failed=`expr $failed + 1`,)
  AC_CHECK_LIB(z,deflate,passed=`expr $passed + 1`,failed=`expr $failed + 1`,)
  AC_CHECK_LIB(z,inflate,passed=`expr $passed + 1`,failed=`expr $failed + 1`,)
  AC_MSG_CHECKING(if ZLIB package is complete)
  if test $passed -gt 0
  then
    if test $failed -gt 0
    then
      AC_MSG_RESULT(no -- some components failed test)
      have_zlib='no (failed tests)'
    else
      LIB_ZLIB='-lz'
      LIBS="$LIB_ZLIB $LIBS"
      AC_DEFINE(HasZLIB)
      AC_MSG_RESULT(yes)
      have_zlib='yes'
    fi
  else
    AC_MSG_RESULT(no)
  fi
fi

#
# Check for BZLIB
#
have_bzlib='no'
if test "$with_bzlib" != 'no'
then
  LIB_BZLIB=''
  AC_CHECKING(for BZLIB support ...)
  failed=0;
  passed=0;
  AC_CHECK_HEADER(bzlib.h,passed=`expr $passed + 1`,failed=`expr $failed + 1`)
  AC_CHECK_LIB(bz2,bzBuffToBuffCompress,passed=`expr $passed + 1`,failed=`expr $failed + 1`,)
  AC_CHECK_LIB(bz2,bzBuffToBuffDecompress,passed=`expr $passed + 1`,failed=`expr $failed + 1`,)
  AC_MSG_CHECKING(if BZLIB package is complete)
  if test $passed -gt 0
  then
    if test $failed -gt 0
    then
      AC_MSG_RESULT(no -- some components failed test)
      have_bzlib='no (failed tests)'
    else
      LIB_BZLIB='-lbz2'
      LIBS="$LIB_BZLIB $LIBS"
      AC_DEFINE(HasBZLIB)
      AC_MSG_RESULT(yes)
      have_bzlib='yes'
    fi
  else
    AC_MSG_RESULT(no)
  fi
fi

#
# Find the X11 include and library directories.
#
AC_PATH_X
AC_PATH_XTRA

LIB_XEXT=''
LIB_XT=''
if test "$with_x" != 'no'
then
    with_x='yes'dnl Ensure value is set
    LDFLAGS="$LDFLAGS $X_LIBS"
    LIBS="-lX11 $X_EXTRA_LIBS $LIBS"
    CPPFLAGS="$CPPFLAGS $X_CFLAGS"
    AC_DEFINE(HasX11)

    AC_CHECK_LIB(Xext,XShmAttach,LIB_XEXT='-lXext',,)
    AC_CHECK_LIB(Xext,XShmAttach,AC_DEFINE(HasSharedMemory),,)
    AC_CHECK_LIB(Xext,XShapeCombineMask,LIB_XEXT='-lXext',,)
    AC_CHECK_LIB(Xext,XShapeCombineMask,AC_DEFINE(HasShape),,)

    AC_CHECK_LIB(Xt,XtSetEventDispatcher,LIB_XT='-lXt',,)
    LIBS="$LIB_XEXT $LIB_XT $LIBS"
fi
AM_CONDITIONAL(HasX11, test "$with_x" != 'no')

#
# If profiling, then check for -ldl and dlopen (required for Solaris & gcc)
#
if test "$with_profiling" = 'yes'
then
  AC_CHECK_LIB(dl,dlopen,LIBS="-ldl $LIBS",,)
fi

#
# Check for Display Postscript
#
have_dps='no'
LIB_DPS=''
if test "$with_dps" != 'no' && test "$with_x" != 'no'
then
    AC_CHECKING([for Display Postscript support ...])
    failed=0;
    passed=0;
    O_CPPFLAGS="$CPPFLAGS"
    CPPFLAGS="$CPPFLAGS -I${ac_x_includes}/X11"
    AC_CHECK_HEADER(DPS/dpsXclient.h,passed=`expr $passed + 1`,failed=`expr $failed + 1`)
    AC_CHECK_HEADER(DPS/dpsXpreview.h,passed=`expr $passed + 1`,failed=`expr $failed + 1`)
    AC_CHECK_LIB(dps,DPSInitialize,passed=`expr $passed + 1`,failed=`expr $failed + 1`,)
    AC_CHECK_LIB(dpstk,XDPSPixelsPerPoint,passed=`expr $passed + 1`,failed=`expr $failed + 1`,-ldps)
    AC_MSG_CHECKING(if DPS package is complete)
    if test $passed -gt 0
    then
    if test $failed -gt 0
    then
	AC_MSG_RESULT([no -- some components failed test])
        have_dps='no (failed tests)'
	CPPFLAGS="$O_CPPFLAGS"
    else
	LIB_DPS='-ldpstk -ldps'
	LIBS="$LIB_DPS $LIBS"
	AC_DEFINE(HasDPS)
	AC_MSG_RESULT(yes)
	have_dps='yes'
    fi
    else
    AC_MSG_RESULT(no)
    CPPFLAGS=$O_CPPFLAGS
    fi
fi


#
# Check for FlashPIX
#
have_fpx='no'
LIB_FPX=''
if test "$with_fpx" != 'no'
then
    AC_CHECKING(for FlashPIX components ...)
    failed=0;
    passed=0;
    AC_CHECK_HEADER(fpx.h,passed=`expr $passed + 1`,failed=`expr $failed + 1`)
    AC_CHECK_LIB(fpx,FPX_OpenImageByFilename,passed=`expr $passed + 1`,failed=`expr $failed + 1`,)
    AC_MSG_CHECKING(if FlashPIX package is complete)
    if test $passed -gt 0
    then
    if test $failed -gt 0
    then
	AC_MSG_RESULT(no -- some components failed test)
	have_fpx='no (failed tests)'
    else
	LIB_FPX='-lfpx'
	LIBS="$LIB_FPX $LIBS"
	AC_DEFINE(HasFPX)
	AC_MSG_RESULT(yes)
        have_fpx='yes'
    fi
    else
        AC_MSG_RESULT(no)
    fi
fi

#
# Check for PNG
#
have_png='no'
LIB_PNG=''
if test "$with_png" != 'no'
then
    AC_CHECKING(for PNG support ...)
    failed=0;
    passed=0;
    AC_CHECK_HEADER(png.h,passed=`expr $passed + 1`,failed=`expr $failed + 1`,)
    AC_CHECK_HEADER(pngconf.h,passed=`expr $passed + 1`,failed=`expr $failed + 1`,)
    AC_CHECK_LIB(png,png_info_init,passed=`expr $passed + 1`,failed=`expr $failed + 1`,)
    AC_MSG_CHECKING(if PNG package is complete)
    if test $passed -gt 0
    then
    if test $failed -gt 0
    then
	AC_MSG_RESULT(no -- some components failed test)
	have_png='no (failed tests)'
    else
	LIB_PNG='-lpng'
	LIBS="$LIB_PNG $LIBS"
	AC_DEFINE(HasPNG)
	AC_MSG_RESULT(yes)
	have_png='yes'
    fi
    else
    AC_MSG_RESULT(no)
    fi
fi

#
# Check for JPEG
#
have_jpeg='no'
LIB_JPEG=''
if test "$with_jpeg" != 'no'
then
    AC_CHECKING(for JPEG support ...)
    failed=0;
    passed=0;
    AC_CHECK_HEADER(jconfig.h,passed=`expr $passed + 1`,failed=`expr $failed + 1`)
    AC_CHECK_HEADER(jerror.h,passed=`expr $passed + 1`,failed=`expr $failed + 1`)
    AC_CHECK_HEADER(jmorecfg.h,passed=`expr $passed + 1`,failed=`expr $failed + 1`)
    AC_CHECK_HEADER(jpeglib.h,passed=`expr $passed + 1`,failed=`expr $failed + 1`)
    AC_CHECK_LIB(jpeg,jpeg_read_header,passed=`expr $passed + 1`,failed=`expr $failed + 1`,)
    AC_MSG_CHECKING(if JPEG package is complete)
    if test $passed -gt 0
    then
    if test $failed -gt 0
    then
	AC_MSG_RESULT(no -- some components failed test)
        have_jpeg='no (failed tests)'
    else
	LIB_JPEG='-ljpeg'
	LIBS="$LIB_JPEG $LIBS"
	AC_DEFINE(HasJPEG)
	AC_MSG_RESULT(yes)
        have_jpeg='yes'
    fi
    else
        AC_MSG_RESULT(no)
    fi
fi

#
# Check for TTF
#
have_ttf='no'
LIB_TTF=''
if test "$with_ttf" != 'no'
then
    AC_CHECKING(for TrueType Font support ...)
    failed=0;
    passed=0;
    AC_CHECK_HEADER(freetype.h,passed=`expr $passed + 1`,failed=`expr $failed + 1`)
    AC_CHECK_LIB(ttf,TT_Init_FreeType,passed=`expr $passed + 1`,failed=`expr $failed + 1`,)
    AC_MSG_CHECKING(if FreeType package is complete)
    if test $passed -gt 0
    then
	if test $failed -gt 0
	then
	    AC_MSG_RESULT(no -- some components failed test)
	    have_ttf='no (failed tests)'
	else
	    LIB_TTF='-lttf'
	    LIBS="$LIB_TTF $LIBS"
	    AC_DEFINE(HasTTF)
	    AC_MSG_RESULT(yes)
	    have_ttf='yes'
	fi
    else
	AC_MSG_RESULT(no)
    fi
fi

#
# Check for TIFF
#
have_tiff='no'
LIB_TIFF=''
if test "$with_tiff" != 'no'
then
    AC_CHECKING(for TIFF support ...)
    failed=0;
    passed=0;
    AC_CHECK_HEADER(tiff.h,passed=`expr $passed + 1`,failed=`expr $failed + 1`)
    AC_CHECK_HEADER(tiffio.h,passed=`expr $passed + 1`,failed=`expr $failed + 1`)
    AC_CHECK_LIB(tiff,TIFFOpen,passed=`expr $passed + 1`,failed=`expr $failed + 1`,)
    AC_MSG_CHECKING(if TIFF package is complete)
    if test $passed -gt 0
    then
    if test $failed -gt 0
    then
	AC_MSG_RESULT(no -- some components failed test)
	have_tiff='no (failed tests)'
    else
	LIB_TIFF='-ltiff'
	LIBS="$LIB_TIFF $LIBS"
	AC_DEFINE(HasTIFF)
	AC_MSG_RESULT(yes)
	have_tiff='yes'
    fi
    else
    AC_MSG_RESULT(no)
    fi
fi

#
# Check for HDF
#
have_hdf='no'
LIB_HDF=''
if test "$with_hdf" != 'no'
then
    AC_CHECKING(for HDF components ...)
    failed=0;
    passed=0;
    AC_CHECK_HEADER(hdf.h,passed=`expr $passed + 1`,failed=`expr $failed + 1`)
    AC_CHECK_LIB(df,DFANputlabel,passed=`expr $passed + 1`,failed=`expr $failed + 1`,)
    AC_MSG_CHECKING(if HDF package is complete)
    if test $passed -gt 0
    then
    if test $failed -gt 0
    then
	AC_MSG_RESULT(no -- some components failed test)
        have_hdf='no (failed tests)'
    else
	LIB_HDF='-ldf'
	LIBS="$LIB_HDF $LIBS"
	AC_DEFINE(HasHDF)
	AC_MSG_RESULT(yes)
	have_hdf='yes'
    fi
    else
    AC_MSG_RESULT(no)
    fi
fi

#
# Check for JBIG
#
have_jbig='no'
LIB_JBIG=''
if test "$with_jbig" != 'no'
then
    AC_CHECKING(for JBIG support ...)
    failed=0;
    passed=0;
    AC_CHECK_HEADER(jbig.h,passed=`expr $passed + 1`,failed=`expr $failed + 1`)
    AC_CHECK_LIB(jbig,jbg_dec_init,passed=`expr $passed + 1`,failed=`expr $failed + 1`,)
    AC_MSG_CHECKING(if JBIG package is complete)
    if test $passed -gt 0
    then
    if test $failed -gt 0
    then
	AC_MSG_RESULT(no -- some components failed test)
        have_jbig='no (failed tests)'
    else
	LIB_JBIG='-ljbig'
	LIBS="$LIB_JBIG $LIBS"
	AC_DEFINE(HasJBIG)
	AC_MSG_RESULT(yes)
        have_jbig='yes'
    fi
    else
        AC_MSG_RESULT(no)
    fi
fi


#
# Configure Delegates
#

# Define directory where ImageMagick/delegates.h lives. (default /usr/local/share)
eval DelegatePath="${DATA_DIR}/ImageMagick/"
AC_DEFINE_UNQUOTED(DelegatePath,"$DelegatePath")

# Locate delegates
AC_PATH_PROG(BZIPDelegate, bzip2, bzip2)
AC_PATH_PROG(BrowseDelegate, netscape, netscape)
AC_PATH_PROG(CGMDecodeDelegate, ralcgm, ralcgm)
AC_PATH_PROG(CatDelegate, cat, cat)
AC_PATH_PROG(DOTDecodeDelegate, dot, dot)
AC_PATH_PROG(DVIDecodeDelegate, dvips, dvips)
AC_PATH_PROG(EchoDelegate, echo, echo)
AC_PATH_PROG(EditorDelegate, xterm, xterm)
AC_PATH_PROG(FIGDecodeDelegate, fig2dev, fig2dev)
AC_PATH_PROG(GnuplotDecodeDelegate, gnuplot, gnuplot)
AC_PATH_PROG(HPGLDecodeDelegate, hp2xx, hp2xx)
AC_PATH_PROG(HTMLDecodeDelegate, html2ps, html2ps)
AC_PATH_PROG(ILBMDecodeDelegate, ilbmtoppm, ilbmtoppm)
AC_PATH_PROG(ILBMEncodeDelegate, ppmtoilbm, ppmtoilbm)
AC_PATH_PROG(LPDelegate, lp, lp)
AC_PATH_PROG(LZWDecodeDelegate, uncompress, uncompress)
AC_PATH_PROG(LZWEncodeDelegate, compress, compress)
AC_PATH_PROG(LaunchDelegate, gimp, gimp)
AC_PATH_PROG(MANDelegate, groff, groff)
AC_PATH_PROG(MPEGDecodeDelegate, mpeg2decode, mpeg2decode)
AC_PATH_PROG(MPEGEncodeDelegate, mpeg2encode, mpeg2encode)
AC_PATH_PROG(MVDelegate, mv, mv)
AC_PATH_PROG(PGPDecodeDelegate, pgpv, pgpv)
AC_PATH_PROG(POVDelegate, povray, povray)
AC_PATH_PROG(PSDelegate, gs, gs)
AC_PATH_PROG(RADDecodeDelegate, ra_ppm, ra_ppm)
AC_PATH_PROG(RLEEncodeDelegate, rawtorle, rawtorle)
AC_PATH_PROG(RMDelegate, rm, rm)
AC_PATH_PROG(SCANDecodeDelegate, scanimage, scanimage)
AC_PATH_PROG(TXTDelegate, enscript, enscript)
AC_PATH_PROG(WMFDecodeDelegate, wmftogif, wmftogif)
AC_PATH_PROG(WWWDecodeDelegate, GET, GET)
AC_PATH_PROG(XCFDecodeDelegate, xcftopnm, xcftopnm)
AC_PATH_PROG(ZipDelegate, gzip, gzip)

# Installed ImageMagick utiltity paths
ConvertDelegate=$BIN_DIR/convert
ShowImageDelegate=$BIN_DIR/display
MogrifyDelegate=$BIN_DIR/mogrify

# Set delegate booleans
have_fig2dev='no' ; if test "$FIGDecodeDelegate" != 'fig2dev' ; then have_fig2dev='yes' ; fi
have_gs='no'      ; if test "$PSDelegate" != 'gs' ; then have_gs='yes' ; fi
have_hp2xx='no'   ; if test "$HPGLDecodeDelegate" != 'hp2xx' ; then have_hp2xx='yes' ; fi
have_ilbmtoppm='no' ; if test "$ILBMDecodeDelegate" != 'ilbmtoppm' ; then have_ilbmtoppm='yes' ; fi
have_ppmtoilbm='no' ; if test "$ILBMEncodeDelegate" != 'ppmtoilbm' ; then have_ppmtoilbm='yes' ; fi
have_mpeg2decode='no' ; if test "$MPEGDecodeDelegate" != 'mpeg2decode' ; then have_mpeg2decode='yes' ; fi
have_mpeg2encode='no' ; if test "$MPEGEncodeDelegate" != 'mpeg2encode' ; then have_mpeg2encode='yes' ; fi
have_ra_ppm='no'  ; if test "$RADDecodeDelegate" != 'ra_ppm' ; then have_ra_ppm='yes' ; fi
have_ralcgm='no'  ; if test "$CGMDecodeDelegate" != 'ralcgm' ; then have_ralcgm='yes' ; fi
have_xcftopnm='no'  ; if test "$XCFDecodeDelegate" != 'xcftopnm' ; then have_xcftopnm='yes' ; fi

if test "$with_frozenpaths" != 'yes'
then
  # Re-set delegate definitions to default (no paths)
  BZIPDelegate='bzip2'
  BrowseDelegate='netscape'
  CGMDecodeDelegate='ralcgm'
  CatDelegate='cat'
  ConvertDelegate='convert'
  DOTDecodeDelegate='dot'
  DVIDecodeDelegate='dvips'
  EchoDelegate='echo'
  EditorDelegate='xterm'
  FIGDecodeDelegate='fig2dev'
  GnuplotDecodeDelegate='gnuplot'
  HPGLDecodeDelegate='hp2xx'
  HTMLDecodeDelegate='html2ps'
  ILBMDecodeDelegate='ilbmtoppm'
  ILBMEncodeDelegate='ppmtoilbm'
  LPDelegate='lp'
  LZWDecodeDelegate='uncompress'
  LZWEncodeDelegate='compress'
  LaunchDelegate='gimp'
  MANDelegate='groff'
  MPEGDecodeDelegate='mpeg2decode'
  MPEGEncodeDelegate='mpeg2encode'
  MVDelegate='mv'
  MogrifyDelegate='mogrify'
  PGPDecodeDelegate='pgpv'
  POVDelegate='povray'
  PSDelegate='gs'
  RADDecodeDelegate='ra_ppm'
  RLEEncodeDelegate='rawtorle'
  RMDelegate='rm'
  SCANDecodeDelegate='scanimage'
  ShowImageDelegate='display'
  TXTDelegate='enscript'
  WMFDecodeDelegate='wmftogif'
  WWWDecodeDelegate='GET'
  XCFDecodeDelegate='xcftopnm'
  ZipDelegate='gzip'
fi

# Delegate substitutions
AC_SUBST(BZIPDelegate)
AC_SUBST(BrowseDelegate)
AC_SUBST(CGMDecodeDelegate)
AC_SUBST(CatDelegate)
AC_SUBST(ConvertDelegate)
AC_SUBST(DOTDecodeDelegate)
AC_SUBST(DVIDecodeDelegate)
AC_SUBST(EchoDelegate)
AC_SUBST(EditorDelegate)
AC_SUBST(FIGDecodeDelegate)
AC_SUBST(GnuplotDecodeDelegate)
AC_SUBST(HPGLDecodeDelegate)
AC_SUBST(HTMLDecodeDelegate)
AC_SUBST(ILBMDecodeDelegate)
AC_SUBST(ILBMEncodeDelegate)
AC_SUBST(LPDelegate)
AC_SUBST(LZWDecodeDelegate)
AC_SUBST(LZWEncodeDelegate)
AC_SUBST(LaunchDelegate)
AC_SUBST(MANDelegate)
AC_SUBST(MPEGDecodeDelegate)
AC_SUBST(MPEGEncodeDelegate)
AC_SUBST(MVDelegate)
AC_SUBST(MogrifyDelegate)
AC_SUBST(PGPDecodeDelegate)
AC_SUBST(POVDelegate)
AC_SUBST(PSDelegate)
AC_SUBST(RADDecodeDelegate)
AC_SUBST(RLEEncodeDelegate)
AC_SUBST(RMDelegate)
AC_SUBST(SCANDecodeDelegate)
AC_SUBST(ShowImageDelegate)
AC_SUBST(TXTDelegate)
AC_SUBST(WMFDecodeDelegate)
AC_SUBST(WWWDecodeDelegate)
AC_SUBST(XCFDecodeDelegate)
AC_SUBST(ZipDelegate)

# Figure out what drivers Ghostscript supports (if available)
if test $have_gs = 'yes'
then

  # GSColorDevice
  AC_MSG_CHECKING(for gs color device)
  if $PSDelegate -q -dBATCH -sDEVICE=pnmraw < /dev/null 2> /dev/null
  then
    GSColorDevice=pnmraw
  else
    if $PSDelegate -q -dBATCH -sDEVICE=ppmraw < /dev/null 2> /dev/null
    then
      GSColorDevice=ppmraw
    else
      GSColorDevice=ppmraw
    fi
  fi
  AC_SUBST(GSColorDevice)
  AC_MSG_RESULT($GSColorDevice)

  # GSMonoDevice
  AC_MSG_CHECKING(for gs mono device)
  if $PSDelegate -q -dBATCH -sDEVICE=pbmraw < /dev/null 2> /dev/null
  then
    GSMonoDevice=pbmraw
  else
    GSMonoDevice=ppmraw
  fi
  AC_SUBST(GSMonoDevice)
  AC_MSG_RESULT($GSMonoDevice)

  # GSPDFDevice
  AC_MSG_CHECKING(for gs PDF writing device)
  if $PSDelegate -q -dBATCH -sDEVICE=pdfwrite < /dev/null 2> /dev/null
  then
    GSPDFDevice=pdfwrite
  else
    GSPDFDevice=nodevice
  fi
  AC_SUBST(GSPDFDevice)
  AC_MSG_RESULT($GSPDFDevice)

  # GSPSDevice
  AC_MSG_CHECKING(for gs PS writing device)
  if $PSDelegate -q -dBATCH -sDEVICE=pswrite < /dev/null 2> /dev/null
  then
    GSPSDevice=pswrite
  else
    GSPSDevice=nodevice
  fi
  AC_SUBST(GSPSDevice)
  AC_MSG_RESULT($GSPSDevice)

  # GSEPSDevice
  AC_MSG_CHECKING(for gs EPS writing device)
  if $PSDelegate -q -dBATCH -sDEVICE=epswrite < /dev/null 2> /dev/null
  then
    GSEPSDevice=epswrite
  else
    GSEPSDevice=nodevice
  fi
  AC_SUBST(GSEPSDevice)
  AC_MSG_RESULT($GSEPSDevice)

fi

#
# PerlMagick-related configuration
#

# Look for PERL if PerlMagick requested
# If name/path of desired PERL interpreter is specified, look for that one first
have_perl='no'
if test "$with_perl" != 'no'
then
    if test "$with_perl" != 'yes'
    then
	AC_CACHE_CHECK(for perl,ac_cv_path_PERL,ac_cv_path_PERL="$with_perl");
	PERL=$ac_cv_path_PERL
	AC_SUBST(PERL)dnl
	have_perl="$ac_cv_path_PERL"
    else
	AC_PATH_PROGS(PERL,perl perl5,)dnl
	if test "$ac_cv_path_PERL"
        then
	  have_perl="$ac_cv_path_PERL"
        fi
    fi
fi

# Determine path to pick up ImageMagick library from for use with building PerlMagick
MAGICKLIB="-L${LIB_DIR} -lMagick"
if test $with_perl_static = 'yes'
then
  MAGICKLIB="-L${builddir}/magick -lMagick"
fi
AC_SUBST(MAGICKLIB)

# Create a simple string containing format names for all delegate libraries
DELEGATES=''
if test "$have_bzlib"  = 'yes' ; then DELEGATES="$DELEGATES bzlib"  ; fi
if test "$have_ralcgm" = 'yes' ; then DELEGATES="$DELEGATES cgm"  ; fi
if test "$have_fpx"    = 'yes' ; then DELEGATES="$DELEGATES fpx"  ; fi
if test "$have_hp2xx"  = 'yes' ; then DELEGATES="$DELEGATES hpgl"  ; fi
if test "$have_hdf"    = 'yes' ; then DELEGATES="$DELEGATES hdf"  ; fi
if test "$have_jbig"   = 'yes' ; then DELEGATES="$DELEGATES jbig" ; fi
if test "$have_jpeg"   = 'yes' ; then DELEGATES="$DELEGATES jpeg" ; fi
if test "$have_mpeg2decode" = 'yes' && test "$have_mpeg2encode"  = 'yes' ; then DELEGATES="$DELEGATES mpeg" ; fi
if test "$have_png"    = 'yes' ; then DELEGATES="$DELEGATES png"  ; fi

have_ps='no'
if test "$have_dps"    = 'yes' || test "$have_gs" = 'yes' ; then have_ps='yes'  ; fi
if test "$have_ps"     = 'yes' ; then DELEGATES="$DELEGATES ps"  ; fi

if test "$have_ra_ppm" = 'yes' ; then DELEGATES="$DELEGATES rad" ; fi
if test "$have_tiff"   = 'yes' ; then DELEGATES="$DELEGATES tiff" ; fi
if test "$have_ttf"    = 'yes' ; then DELEGATES="$DELEGATES ttf"  ; fi
if test "$have_x"      = 'yes' ; then DELEGATES="$DELEGATES x"    ; fi
if test "$have_fig2dev" = 'yes' && test "$have_ps" = 'yes'  ; then DELEGATES="$DELEGATES xfig"    ; fi
if test "$have_zlib"   = 'yes' ; then DELEGATES="$DELEGATES zlib" ; fi

AC_SUBST(DELEGATES)

# Check for networking libraries and definitions needed by xtp
#
# This code was obtained from the 'expect' package written by Don Libes of NIST
# which contains statements similar to: Design and implementation of this
# program was paid for by U.S. tax dollars.  Therefore it is public domain.
# However, the author and NIST would appreciate credit if this program or parts
# of it are used.
#
# Look for various features to determine what kind of pty
# we have. For some weird reason, ac_compile_check would not
# work, but ac_test_program does.
#

AC_MSG_CHECKING([for HP style pty allocation])
# following test fails on DECstations and other things that don't grok -c
# but that's ok, since they don't have PTYMs anyway
if test -r /dev/ptym/ptyp0 2>/dev/null ; then
    AC_MSG_RESULT(yes)
    AC_DEFINE(HAVE_PTYM)
    AC_MSG_WARN([HP style ptys not yet supported.])
else
    AC_MSG_RESULT(no)
fi

AC_MSG_CHECKING([for HP style pty trapping])
AC_HEADER_EGREP([struct.*request_info], sys/ptyio.h,
        AC_MSG_RESULT(yes)
        AC_DEFINE(HAVE_PTYTRAP)
,
        AC_MSG_RESULT(no)
)

AC_MSG_CHECKING([for AIX new-style pty allocation])
if test -r /dev/ptc -a -r /dev/pts ; then
    AC_MSG_RESULT(yes)
    AC_DEFINE(HAVE_PTC_PTS)
    AC_MSG_WARN([AIX new-style ptys not yet supported.])
else
    AC_MSG_RESULT(no)
fi

AC_MSG_CHECKING([for SGI old-style pty allocation])
if test -r /dev/ptc -a ! -r /dev/pts ; then
    AC_MSG_RESULT(yes)
    AC_DEFINE(HAVE_PTC)
    AC_MSG_WARN([SGI old-style ptys not yet supported.])
else
    AC_MSG_RESULT(no)
fi

# On SCO OpenServer, two types of ptys are available: SVR4 streams and c-list.
# The library routines to open the SVR4 ptys are broken on certain systems and
# the SCO command to increase the number of ptys only configure c-list ones
# anyway.  So we chose these, which have a special numbering scheme.
#
AC_MSG_CHECKING([for SCO style pty allocation])
sco_ptys=""
case "${host}" in
  *-sco3.2v[[45]]*)     sco_clist_ptys=1 svr4_ptys_broken=1;;
esac

if test x"${sco_clist_ptys}" != x"" ; then
  AC_MSG_RESULT(yes)
  AC_DEFINE(HAVE_SCO_CLIST_PTYS)
  AC_MSG_WARN([SCO Clist ptys not yet supported.])
else
  AC_MSG_RESULT(no)
fi

AC_MSG_CHECKING([for SVR4/UNIX-'98 style pty allocation])
if test -r /dev/ptmx -a "x$svr4_ptys_broken" = x ; then
  AC_MSG_RESULT(yes)
  AC_DEFINE(HAVE_PTMX)
  # aargg. Some systems need libpt.a to use /dev/ptmx
  AC_CHECK_FUNC(ptsname, , LIBS="${LIBS} -lpt")
else
  AC_MSG_RESULT(no)
fi

# In OSF/1 case, SVR4 are somewhat different.
# Gregory Depp <depp@osf.org> 17Aug93
AC_MSG_CHECKING([for OSF/1 style pty allocation])
if test -r /dev/ptmx_bsd ; then
    AC_DEFINE(HAVE_PTMX_BSD)
    AC_MSG_WARN([OSF/1 style ptys not yet supported.])
    AC_MSG_RESULT(yes)
else
    AC_MSG_RESULT(no)
fi

#
# End of pty checks lifted from 'expect'
#

have_socks='no'
XTP_LIBS="$XTP_LIBS $LIBS_INITIAL"
XTP_LIBS="$LIB_SFIO $XTP_LIBS"
XTP_DEFS=''
XTP_FTP='ftp'

AC_CHECK_LIB(socket,socket,XTP_LIBS="-lsocket $XTP_LIBS",,)
AC_CHECK_LIB(nsl,gethostbyname,XTP_LIBS="-lnsl $XTP_LIBS",,)
# If profiling, then check for -ldl and dlopen (required for Solaris & gcc)
if test "$with_profiling" = 'yes'
then
  AC_CHECK_LIB(dl,dlopen,XTP_LIBS="-ldl $XTP_LIBS",,)
fi
if test "$with_socks" = 'yes'
then
  have_socks_header='no'
  have_socks_lib='no'
  AC_CHECK_HEADER(socks.h,have_socks_header='yes',have_socks_header='no')
  AC_CHECK_LIB(socks5,SOCKSinit,have_socks_lib='yes',have_socks_lib='no',)
  if test $have_socks_header = 'yes' && test $have_socks_lib = 'yes'
  then
    XTP_LIBS="-lsocks5 $XTP_LIBS"
    XTP_DEFS="-DSOCKS $XTP_DEFS "
    have_socks='yes'
  fi
  AC_PATH_PROGS(XTP_FTP,rftp ftp,,)
fi

XTP_DEFS="$XTP_DEFS -DXTP_FTP=\"\\\"$XTP_FTP\\\"\""
AC_SUBST(XTP_LIBS)dnl
AC_SUBST(XTP_DEFS)dnl

# Add '-p' if prof source profiling support enabled
if test "$with_prof" = 'yes'
then
  CFLAGS="-p $CFLAGS"
  LDFLAGS="-p $LDFLAGS"
fi

# Add '-pg' if gprof source profiling support enabled
if test "$with_gprof" = 'yes'
then
  CFLAGS="-pg $CFLAGS"
  LDFLAGS="-pg $LDFLAGS"
fi

# Add '-ftest-coverage -fprofile-arcs' if gcov source profiling support enabled
# This is a gcc-specific feature
if test "$with_gcov" = 'yes'
then
  CFLAGS="-ftest-coverage -fprofile-arcs  $CFLAGS"
  LDFLAGS="-ftest-coverage -fprofile-arcs $LDFLAGS"
fi

#
# Remove extraneous spaces from output variables (asthetic)
#
X_CFLAGS=`echo $X_CFLAGS | sed -e 's/  */ /g'`
X_PRE_LIBS=`echo $X_PRE_LIBS | sed -e 's/  */ /g'`
X_LIBS=`echo $X_LIBS | sed -e 's/  */ /g'`
X_EXTRA_LIBS=`echo $X_EXTRA_LIBS | sed -e 's/  */ /g'`

CC=`echo $CC | sed -e 's/  */ /g'`
CFLAGS=`echo $CFLAGS | sed -e 's/  */ /g'`
CPPFLAGS=`echo $CPPFLAGS | sed -e 's/  */ /g'`
LDFLAGS=`echo $LDFLAGS | sed -e 's/  */ /g'`
LIBS=`echo $LIBS | sed -e 's/  */ /g'`

AC_SUBST(CPPFLAGS)
AC_SUBST(X_CFLAGS)
AC_SUBST(LDFLAGS)
AC_SUBST(X_PRE_LIBS)
AC_SUBST(X_LIBS)
AC_SUBST(X_EXTRA_LIBS)

AC_OUTPUT(Makefile delegates/delegates.mgk delegates/Makefile magick/Makefile magick/Magick-config xtp/Makefile PerlMagick/Makefile.PL)

echo ""

echo "ImageMagick is configured as follows. Please verify that this"
echo "configuration matches your expectations."
echo ""
echo "Host system type : $host"
echo ""
echo "Option            Configure option        Configured value"
echo "----------------------------------------------------------"
echo "Shared libraries  --enable-shared=$enable_shared		$libtool_build_shared_libs"
echo "Static libraries  --enable-static=$enable_static		$libtool_build_static_libs"
echo "GNU ld            --with-gnu-ld=$with_gnu_ld		$ac_cv_prog_gnu_ld"
echo "Ghostscript       None                          $have_gs"
echo "LZW support       --enable-lzw=$with_lzw		$with_lzw"
echo "16 bit pixels     --enable-16bit-pixel=$with_16_bit_pixel	$with_16_bit_pixel"
echo "SFIO              --enable-sfio=$with_sfio		$have_sfio"
echo "SOCKS             --enable-socks=$with_socks		$have_socks"
echo "PERL              --with-perl=$with_perl		$have_perl"
echo "BZLIB             --with-bzlib=$with_bzlib		$with_bzlib"
echo "DPS               --with-dps=$with_dps		$have_dps"
echo "FlashPIX          --with-fpx=$with_fpx		$have_fpx"
echo "HDF               --with-hdf=$with_hdf		$have_hdf"
echo "JBIG              --with-jbig=$with_jbig		$have_jbig"
echo "JPEG              --with-jpeg=$with_jpeg		$have_jpeg"
echo "PNG               --with-png=$with_png		$have_png"
echo "TIFF              --with-tiff=$with_tiff		$have_tiff"
echo "TrueType (TTF)    --with-ttf=$with_ttf		$have_ttf"
echo "TTF Path          --with-ttf-fontpath=          \"$with_ttf_fontpath\""
echo "ZLIB              --with-zlib=$with_zlib		$have_zlib"
echo "X11               --with-x=$with_x			$have_x"
echo ""
echo "X11 Configuration:"
if test "$have_x" != 'no'
then
  echo "  X_CFLAGS     = $X_CFLAGS"
  echo "  X_PRE_LIBS   = $X_PRE_LIBS"
  echo "  X_LIBS       = $X_LIBS"
  echo "  X_EXTRA_LIBS = $X_EXTRA_LIBS"
else
  echo ""
  echo "  Not using X11."
fi
echo ""
echo "Final Compilation Flags:"
echo "  CC       = $CC"
echo "  CFLAGS   = $CFLAGS"
echo "  CPPFLAGS = $CPPFLAGS"
echo "  LDFLAGS  = $LDFLAGS"
echo "  LIBS     = $LIBS"
echo ""
