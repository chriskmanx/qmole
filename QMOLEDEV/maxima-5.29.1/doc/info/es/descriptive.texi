@c English version: 2012-06-03
@menu
* Introducci@'on a descriptive::
* Funciones y variables para el tratamiento de datos::
* Funciones y variables de par@'ametros descriptivos::
* Funciones y variables para gr@'aficos estad@'{@dotless{i}}sticos::
@end menu

@node Introducci@'on a descriptive, Funciones y variables para el tratamiento de datos, descriptive, descriptive
@section Introducci@'on a descriptive


El paquete @code{descriptive} contiene funciones para realizar 
c@'lculos y gr@'aficos estad@'{@dotless{i}}sticos descriptivos. 
Junto con el c@'odigo fuente se distribuyen tres conjuntos de datos: 
@code{pidigits.data}, @code{wind.data} y @code{biomed.data}.

Cualquier manual de estad@'{@dotless{i}}stica se puede utilizar como referencia al paquete @code{descriptive}.

Para comentarios, fallos y sugerencias, por favor contactar con @var{'mario AT edu DOT xunta DOT es'}.

Aqu@'{@dotless{i}} un sencillo ejemplo sobre c@'omo operan las funciones de @code{descriptive}, dependiendo de la naturaleza de sus argumentos, listas o matrices,

@c ===beg===
@c load (descriptive)$
@c /* muestra univariate */   mean ([a, b, c]);
@c matrix ([a, b], [c, d], [e, f]);
@c /* muestra multivariante */ mean (%);
@c ===end===
@example
(%i1) load (descriptive)$
@group
(%i2) /* muestra univariate */   mean ([a, b, c]);
                            c + b + a
(%o2)                       ---------
                                3
@end group
@group
(%i3) matrix ([a, b], [c, d], [e, f]);
                            [ a  b ]
                            [      ]
(%o3)                       [ c  d ]
                            [      ]
                            [ e  f ]
@end group
@group
(%i4) /* muestra multivariante */ mean (%);
                      e + c + a  f + d + b
(%o4)                [---------, ---------]
                          3          3
@end group
@end example

N@'otese que en las muestras multivariantes la media se calcula para cada columna.

En caso de varias muestras de diferente tama@~no, la funci@'on @code{map} de Maxima puede utilizarse para obtener los resultados deseados para cada muestra,

@c ===beg===
@c load (descriptive)$
@c map (mean, [[a, b, c], [d, e]]);
@c ===end===
@example
(%i1) load (descriptive)$
@group
(%i2) map (mean, [[a, b, c], [d, e]]);
                        c + b + a  e + d
(%o2)                  [---------, -----]
                            3        2
@end group
@end example

En este caso, dos muestras de tama@~nos 3 y 2 han sido almacenadas en una lista.

Muestras univariantes deben guardarse en listas como en

@c ===beg===
@c s1 : [3, 1, 4, 1, 5, 9, 2, 6, 5, 3, 5];
@c ===end===
@example
@group
(%i1) s1 : [3, 1, 4, 1, 5, 9, 2, 6, 5, 3, 5];
(%o1)           [3, 1, 4, 1, 5, 9, 2, 6, 5, 3, 5]
@end group
@end example

y muestras multivariantes en matrices como las del siguiente ejemplo

@c ===beg===
@c s2 : matrix ([13.17, 9.29], [14.71, 16.88], [18.50, 16.88],
@c              [10.58, 6.63], [13.33, 13.25], [13.21,  8.12]);
@c ===end===
@example
(%i1) s2 : matrix ([13.17, 9.29], [14.71, 16.88], [18.50, 16.88],
             [10.58, 6.63], [13.33, 13.25], [13.21,  8.12]);
                        [ 13.17  9.29  ]
                        [              ]
                        [ 14.71  16.88 ]
                        [              ]
                        [ 18.5   16.88 ]
(%o1)                   [              ]
                        [ 10.58  6.63  ]
                        [              ]
                        [ 13.33  13.25 ]
                        [              ]
                        [ 13.21  8.12  ]
@end example

En este caso, el n@'umero de columnas es igual al de la dimensi@'on de la variable aleatoria y el n@'umero de filas coincide con el tama@~no muestral.

Los datos pueden suministrarse manualmente, pero las muestras grandes se suelen almacenar en ficheros de texto. Por ejemplo, el fichero @code{pidigits.data} contiene los 100 primeros d@'{@dotless{i}}gitos del n@'umero @code{%pi}:
@example
      3
      1
      4
      1
      5
      9
      2
      6
      5
      3 ...
@end example

A fin de leer estos d@'{@dotless{i}}gitos desde Maxima,

@c ===beg===
@c s1 : read_list (file_search ("pidigits.data"))$
@c length (s1);
@c ===end===
@example
(%i1) s1 : read_list (file_search ("pidigits.data"))$
@group
(%i2) length (s1);
(%o2)                          100
@end group
@end example

Por otro lado, el archivo @code{wind.data} contiene los promedios diarios de la velocidad del viento en cinco estaciones meteorol@'ogicas en Irlanda (esta muestra es parte de un conjunto de datos correspondientes a 12 estaciones meteorol@'ogicas. El fichero original se puede descargar libremente del 'StatLib Data Repository' y se analiza en  Haslett, J., Raftery, A. E. (1989) @var{Space-time Modelling with Long-memory Dependence: Assessing Ireland's Wind Power Resource, with Discussion}. Applied Statistics 38, 1-50). As@'{@dotless{i}} se leen los datos:

@c ===beg===
@c s2 : read_matrix (file_search ("wind.data"))$
@c length (s2);
@c s2 [%]; /* last record */
@c ===end===
@example
(%i1) s2 : read_matrix (file_search ("wind.data"))$
@group
(%i2) length (s2);
(%o2)                          100
@end group
@group
(%i3) s2 [%]; /* last record */
(%o3)            [3.58, 6.0, 4.58, 7.62, 11.25]
@end group
@end example

Algunas muestras contienen datos no num@'ericos. Como ejemplo, el archivo @code{biomed.data} (el cual es parte de otro mayor descargado tambi@'en del 'StatLib Data Repository') contiene cuatro mediciones sangu@'{@dotless{i}}neas tomadas a dos grupos de pacientes, @code{A} y @code{B}, de diferentes edades,

@c ===beg===
@c s3 : read_matrix (file_search ("biomed.data"))$
@c length (s3);
@c s3 [1]; /* first record */
@c ===end===
@example
(%i1) s3 : read_matrix (file_search ("biomed.data"))$
@group
(%i2) length (s3);
(%o2)                          100
@end group
@group
(%i3) s3 [1]; /* first record */
(%o3)            [A, 30, 167.0, 89.0, 25.6, 364]
@end group
@end example

El primer individuo pertenece al grupo @code{A}, tiene 30 a@~nos de edad y sus medidas sangu@'{@dotless{i}}neas fueron 167.0, 89.0, 25.6 y 364.

Debe tenerse cuidado cuando se trabaje con datos categ@'oricos. En el siguiente ejemplo, se asigna al s@'{@dotless{i}}mbolo @code{a} cierto valor en alg@'un momento previo y luego se toma una muestra con el valor categ@'orico @code{a},

@c ===beg===
@c a : 1$
@c matrix ([a, 3], [b, 5]);
@c ===end===
@example
(%i1) a : 1$
@group
(%i2) matrix ([a, 3], [b, 5]);
                            [ 1  3 ]
(%o2)                       [      ]
                            [ b  5 ]
@end group
@end example


@node Funciones y variables para el tratamiento de datos, Funciones y variables de par@'ametros descriptivos, Introducci@'on a descriptive, descriptive
@section Funciones y variables para el tratamiento de datos


@deffn {Funci@'on} continuous_freq (@var{list})
@deffnx {Funci@'on} continuous_freq (@var{list}, @var{m})
El argumento de @code{continuous_freq} debe ser una lista de n@'umeros.
Divide el rango en intervalos y cuenta cu@'antos valores hay en ellos.
El segundo argumento es opcional y puede ser el n@'umero de clases 
deseado, 10 por defecto, o una lista que contenga los l@'{@dotless{i}}mites
de las clases y el n@'umero de @'estas, o una lista que contenga
@'unicamente los l@'{@dotless{i}}mites. Si los valores muestrales
son todos iguales, esta funci@'on devuelve solamente una clase 
de amplitud 2.

Ejemplos:

El argumento opcional indica el n@'umero de clases deseadas. 
La primera lista de la respuesta contiene los l@'{@dotless{i}}mites
de los intervalos y la segunda los totales correspondientes: hay 16
d@'{@dotless{i}}gitos en el intervalo @code{[0, 1.8]}, 24 en 
@code{(1.8, 3.6]} y as@'{@dotless{i}} sucesivamente.

@c ===beg===
@c load (descriptive)$
@c s1 : read_list (file_search ("pidigits.data"))$
@c continuous_freq (s1, 5);
@c ===end===
@example
(%i1) load (descriptive)$
(%i2) s1 : read_list (file_search ("pidigits.data"))$
(%i3) continuous_freq (s1, 5);
(%o3) [[0, 1.8, 3.6, 5.4, 7.2, 9.0], [16, 24, 18, 17, 25]]
@end example

El argumento opcional indica que queremos 7 clases con 
l@'{@dotless{i}}mites -2 y 12:

@c ===beg===
@c load (descriptive)$
@c s1 : read_list (file_search ("pidigits.data"))$
@c continuous_freq (s1, [-2,12,7]);
@c ===end===
@example
(%i1) load (descriptive)$
(%i2) s1 : read_list (file_search ("pidigits.data"))$
(%i3) continuous_freq (s1, [-2,12,7]);
(%o3) [[- 2, 0, 2, 4, 6, 8, 10, 12], [8, 20, 22, 17, 20, 13, 0]]
@end example

El argumento opcional indica que queremos el n@'umero por defecto
de clases y l@'{@dotless{i}}mites -2 y 12:

@c ===beg===
@c load (descriptive)$
@c s1 : read_list (file_search ("pidigits.data"))$
@c continuous_freq (s1, [-2,12]);
@c ===end===
@example
(%i1) load (descriptive)$
(%i2) s1 : read_list (file_search ("pidigits.data"))$
(%i3) continuous_freq (s1, [-2,12]);
                3  4  11  18     32  39  46  53
(%o3)  [[- 2, - -, -, --, --, 5, --, --, --, --, 12], 
                5  5  5   5      5   5   5   5
               [0, 8, 20, 12, 18, 9, 8, 25, 0, 0]]
@end example

@end deffn



@deffn {Funci@'on} discrete_freq (@var{list})
Calcula las frecuencias absolutas en muestras discretas, tanto num@'ericas como categ@'oricas. Su @'unico argumento debe ser una lista.

@c ===beg===
@c load (descriptive)$
@c s1 : read_list (file_search ("pidigits.data"))$
@c discrete_freq (s1);
@c ===end===
@example
(%i1) load (descriptive)$
(%i2) s1 : read_list (file_search ("pidigits.data"))$
@group
(%i3) discrete_freq (s1);
(%o3) [[0, 1, 2, 3, 4, 5, 6, 7, 8, 9], 
                             [8, 8, 12, 12, 10, 8, 9, 8, 12, 13]]
@end group
@end example


La primera lista son los valores de la muestra y la segunda sus frecuencias absolutas. Las instrucciones @code{? col} y 
@code{? transpose} pueden ayudar a comprender la @'ultima entrada.
@end deffn



@deffn {Funci@'on} standardize (@var{list})
@deffnx {Funci@'on} standardize (@var{matrix})

Resta a cada elemento de la lista la media muestral y luego divide el resultado por
la desviaci@'on t@'{@dotless{i}}pica. Si la entrada es una matriz, @code{standardize} 
resta a cada fila la media multivariante y luego divide cada componente por la
desviaci@'on t@'{@dotless{i}}pica correspondiente.

@end deffn




@deffn {Funci@'on} subsample (@var{data_matrix}, @var{predicate_function})
@deffnx {Funci@'on} subsample (@var{data_matrix}, @var{predicate_function}, @var{col_num}, @var{col_num}, ...)
Esta es una variante de la funci@'on @code{submatrix} de Maxima.
El primer argumento es una matriz de datos, el segundo es una 
funci@'on de predicado y el resto de argumentos opcionales son
los n@'umeros de las columnas a tomar en consideraci@'on.

Estos son los registros multivariantes en los que la velocidad del viento
en la primera estaci@'on meteorol@'ogica fue menor de 18 nudos. V@'ease 
c@'omo en la expresi@'on lambda la @var{i}-@'esima componente 
se la referencia como @code{v[i]}.
@c ===beg===
@c load (descriptive)$
@c s2 : read_matrix (file_search ("wind.data"))$
@c subsample (s2, lambda([v], v[1] > 18));
@c ===end===
@example
(%i1) load (descriptive)$
(%i2) s2 : read_matrix (file_search ("wind.data"))$
@group
(%i3) subsample (s2, lambda([v], v[1] > 18));
              [ 19.38  15.37  15.12  23.09  25.25 ]
              [                                   ]
              [ 18.29  18.66  19.08  26.08  27.63 ]
(%o3)         [                                   ]
              [ 20.25  21.46  19.95  27.71  23.38 ]
              [                                   ]
              [ 18.79  18.96  14.46  26.38  21.84 ]
@end group
@end example

En el siguiente ejemplo, se solicitan @'unicamente la primera, segunda y
quinta componentes de aquellos registros con velocidades del viento mayores
o iguales que 16 nudos en la estaci@'on n@'umero 1 y menores que 25 nudos
en la estaci@'on n@'umero 4. La muestra s@'olo contiene los datos referidos
a las estaciones 1, 2 y 5. En este caso, la funci@'on de predicado se
define por medio de una funci@'on de Maxima ordinaria.
@c ===beg===
@c load (descriptive)$
@c s2 : read_matrix (file_search ("wind.data"))$
@c g(x):= x[1] >= 16 and x[4] < 25$
@c subsample (s2, g, 1, 2, 5);
@c ===end===
@example
(%i1) load (descriptive)$
(%i2) s2 : read_matrix (file_search ("wind.data"))$
(%i3) g(x):= x[1] >= 16 and x[4] < 25$
@group
(%i4) subsample (s2, g, 1, 2, 5);
                     [ 19.38  15.37  25.25 ]
                     [                     ]
                     [ 17.33  14.67  19.58 ]
(%o4)                [                     ]
                     [ 16.92  13.21  21.21 ]
                     [                     ]
                     [ 17.25  18.46  23.87 ]
@end group
@end example

He aqu@'{@dotless{i}} un ejemplo con las variables categ@'oricas de @code{biomed.data}.
Se piden los registros correspondientes a aquellos pacientes del grupo @code{B} mayores de 38 an@~nos,
@c ===beg===
@c load (descriptive)$
@c s3 : read_matrix (file_search ("biomed.data"))$
@c h(u):= u[1] = B and u[2] > 38 $
@c subsample (s3, h);
@c ===end===
@example
(%i1) load (descriptive)$
(%i2) s3 : read_matrix (file_search ("biomed.data"))$
(%i3) h(u):= u[1] = B and u[2] > 38 $
(%i4) subsample (s3, h);
                [ B  39  28.0  102.3  17.1  146 ]
                [                               ]
                [ B  39  21.0  92.4   10.3  197 ]
                [                               ]
                [ B  39  23.0  111.5  10.0  133 ]
                [                               ]
                [ B  39  26.0  92.6   12.3  196 ]
(%o4)           [                               ]
                [ B  39  25.0  98.7   10.0  174 ]
                [                               ]
                [ B  39  21.0  93.2   5.9   181 ]
                [                               ]
                [ B  39  18.0  95.0   11.3  66  ]
                [                               ]
                [ B  39  39.0  88.5   7.6   168 ]
@end example

Es probable que el an@'alisis estad@'{@dotless{i}}stico
requiera @'unicamente de las medidas sangu@'{@dotless{i}}neas.
@c ===beg===
@c load (descriptive)$
@c s3 : read_matrix (file_search ("biomed.data"))$
@c subsample (s3, lambda([v], v[1] = B and v[2] > 38),
@c            3, 4, 5, 6);
@c ===end===
@example
(%i1) load (descriptive)$
(%i2) s3 : read_matrix (file_search ("biomed.data"))$
(%i3) subsample (s3, lambda([v], v[1] = B and v[2] > 38),
           3, 4, 5, 6);
                   [ 28.0  102.3  17.1  146 ]
                   [                        ]
                   [ 21.0  92.4   10.3  197 ]
                   [                        ]
                   [ 23.0  111.5  10.0  133 ]
                   [                        ]
                   [ 26.0  92.6   12.3  196 ]
(%o3)              [                        ]
                   [ 25.0  98.7   10.0  174 ]
                   [                        ]
                   [ 21.0  93.2   5.9   181 ]
                   [                        ]
                   [ 18.0  95.0   11.3  66  ]
                   [                        ]
                   [ 39.0  88.5   7.6   168 ]
@end example

Esta es la media multivariante de @code{s3}.
@c ===beg===
@c load (descriptive)$
@c s3 : read_matrix (file_search ("biomed.data"))$
@c mean (s3);
@c ===end===
@example
(%i1) load (descriptive)$
(%i2) s3 : read_matrix (file_search ("biomed.data"))$
@group
(%i3) mean (s3);
       65 B + 35 A  317          6 NA + 8145.0
(%o3) [-----------, ---, 87.178, -------------, 18.123, 
           100      10                100
                                                    3 NA + 19587
                                                    ------------]
                                                        100
@end group
@end example

Aqu@'{@dotless{i}} la primera componente carece de significado, ya que tanto @code{A} como @code{B} son categ@'oricas, la segunda componente es la edad media de los individuos en forma racional, al tiempo que los valores cuarto y quinto muestran cierto comportamiento extra@~no; lo cual se debe a que el s@'{@dotless{i}}mbolo @code{NA} se utiliza para indicar datos @i{no disponibles}, por lo que ambas medias no tienen sentido. Una posible soluci@'on puede ser extraer de la matriz aquellas filas con s@'{@dotless{i}}mbolos @code{NA}, lo que acarrear@'{@dotless{i}}a cierta p@'erdida de informaci@'on.
@c ===beg===
@c load (descriptive)$
@c s3 : read_matrix (file_search ("biomed.data"))$
@c g(v):= v[4] # NA and v[6] # NA $
@c mean (subsample (s3, g, 3, 4, 5, 6));
@c ===end===
@example
(%i1) load (descriptive)$
(%i2) s3 : read_matrix (file_search ("biomed.data"))$
(%i3) g(v):= v[4] # NA and v[6] # NA $
@group
(%i4) mean (subsample (s3, g, 3, 4, 5, 6));
(%o4) [79.4923076923077, 86.2032967032967, 16.93186813186813, 
                                                            2514
                                                            ----]
                                                             13
@end group
@end example
@end deffn





@deffn {Funci@'on} tranform_sample (@var{matriz}, @var{varlist}, @var{exprlist})

Transforma la @var{matriz} de datos, en la que a cada columna se le asigna
un nombre de acuerdo con la lista @var{varlist}, seg@'un las expresiones de @var{exprlist}.

Ejemplos:

El segundo argumento asigna nombres a las tres columnas, con ellos la lista de expresiones
define la transformaci@'on de la muestra.

@example
(%i1) load (descriptive)$
(%i2) data: matrix([3,2,7],[3,7,2],[8,2,4],[5,2,4]) $
@group
(%i3) tranform_sample(data, [a,b,c], [c, a*b, log(a)]);
                               [ 7  6   log(3) ]
                               [               ]
                               [ 2  21  log(3) ]
(%o3)                          [               ]
                               [ 4  16  log(8) ]
                               [               ]
                               [ 4  10  log(5) ]
@end group
@end example

Add a constant column and remove the third variable.

@example
(%i1) load (descriptive)$
(%i2) data: matrix([3,2,7],[3,7,2],[8,2,4],[5,2,4]) $
(%i3) tranform_sample(data, [a,b,c], [makelist(1,k,length(data)),a,b]);
@group
                                  [ 1  3  2 ]
                                  [         ]
                                  [ 1  3  7 ]
(%o3)                             [         ]
                                  [ 1  8  2 ]
                                  [         ]
                                  [ 1  5  2 ]
@end group
@end example
@end deffn





@node Funciones y variables de par@'ametros descriptivos, Funciones y variables para gr@'aficos estad@'{@dotless{i}}sticos, Funciones y variables para el tratamiento de datos, descriptive
@section Funciones y variables de par@'ametros descriptivos


@deffn {Funci@'on} mean (@var{list})
@deffnx {Funci@'on} mean (@var{matrix})
Es la media muestral, definida como
@ifnottex
@example
                       n
                     ====
             _   1   \
             x = -    >    x
                 n   /      i
                     ====
                     i = 1
@end example
@end ifnottex
@tex
$${\bar{x}={1\over{n}}{\sum_{i=1}^{n}{x_{i}}}}$$
@end tex


Ejemplo:

@c ===beg===
@c load (descriptive)$
@c s1 : read_list (file_search ("pidigits.data"))$
@c mean (s1);
@c %, numer;
@c s2 : read_matrix (file_search ("wind.data"))$
@c mean (s2);
@c ===end===
@example
(%i1) load (descriptive)$
(%i2) s1 : read_list (file_search ("pidigits.data"))$
@group
(%i3) mean (s1);
                               471
(%o3)                          ---
                               100
@end group
@group
(%i4) %, numer;
(%o4)                         4.71
@end group
(%i5) s2 : read_matrix (file_search ("wind.data"))$
@group
(%i6) mean (s2);
(%o6)     [9.9485, 10.1607, 10.8685, 15.7166, 14.8441]
@end group
@end example

@end deffn



@deffn {Funci@'on} var (@var{list})
@deffnx {Funci@'on} var (@var{matrix})
Es la varianza muestral, definida como
@ifnottex
@example
                     n
                   ====
           2   1   \          _ 2
          s  = -    >    (x - x)
               n   /       i
                   ====
                   i = 1
@end example
@end ifnottex
@tex
$${{1}\over{n}}{\sum_{i=1}^{n}{(x_{i}-\bar{x})^2}}$$
@end tex


Ejemplo:

@c ===beg===
@c load (descriptive)$
@c s1 : read_list (file_search ("pidigits.data"))$
@c var (s1), numer;
@c ===end===
@example
(%i1) load (descriptive)$
(%i2) s1 : read_list (file_search ("pidigits.data"))$
@group
(%i3) var (s1), numer;
(%o3)                   8.425899999999999
@end group
@end example

V@'ease tambi@'en @code{var1}.
@end deffn



@deffn {Funci@'on} var1 (@var{list})
@deffnx {Funci@'on} var1 (@var{matrix})
Es la cuasivarianza muestral, definida como
@ifnottex
@example
                     n
                   ====
               1   \          _ 2
              ---   >    (x - x)
              n-1  /       i
                   ====
                   i = 1
@end example
@end ifnottex
@tex
$${{1\over{n-1}}{\sum_{i=1}^{n}{(x_{i}-\bar{x})^2}}}$$
@end tex


Ejemplo:

@c ===beg===
@c load (descriptive)$
@c s1 : read_list (file_search ("pidigits.data"))$
@c var1 (s1), numer;
@c s2 : read_matrix (file_search ("wind.data"))$
@c var1 (s2);
@c ===end===
@example
(%i1) load (descriptive)$
(%i2) s1 : read_list (file_search ("pidigits.data"))$
@group
(%i3) var1 (s1), numer;
(%o3)                    8.5110101010101
@end group
(%i4) s2 : read_matrix (file_search ("wind.data"))$
@group
(%i5) var1 (s2);
(%o5) [17.39586540404041, 15.13912778787879, 15.63204924242424, 
                            32.50152569696971, 24.66977392929294]
@end group
@end example

V@'ease tambi@'en @code{var}.
@end deffn



@deffn {Funci@'on} std (@var{list})
@deffnx {Funci@'on} std (@var{matrix})
Es la desviaci@'on t@'{@dotless{i}}pica muestral, ra@'{@dotless{i}}z cuadrada de @code{var}.

Ejemplo:

@c ===beg===
@c load (descriptive)$
@c s1 : read_list (file_search ("pidigits.data"))$
@c std (s1), numer;
@c s2 : read_matrix (file_search ("wind.data"))$
@c std (s2);
@c ===end===
@example
(%i1) load (descriptive)$
(%i2) s1 : read_list (file_search ("pidigits.data"))$
@group
(%i3) std (s1), numer;
(%o3)                   2.902740084816414
@end group
(%i4) s2 : read_matrix (file_search ("wind.data"))$
@group
(%i5) std (s2);
(%o5) [4.149928523480858, 3.871399812729241, 3.933920277534866, 
                            5.672434260526957, 4.941970881136392]
@end group
@end example

V@'eanse tambi@'en @code{var} y @code{std1}.
@end deffn



@deffn {Funci@'on} std1 (@var{list})
@deffnx {Funci@'on} std1 (@var{matrix})
Es la cuasidesviaci@'on t@'{@dotless{i}}pica muestral, ra@'{@dotless{i}}z cuadrada de @code{var1}.

Ejemplo:

@c ===beg===
@c load (descriptive)$
@c s1 : read_list (file_search ("pidigits.data"))$
@c std1 (s1), numer;
@c s2 : read_matrix (file_search ("wind.data"))$
@c std1 (s2);
@c ===end===
@example
(%i1) load (descriptive)$
(%i2) s1 : read_list (file_search ("pidigits.data"))$
@group
(%i3) std1 (s1), numer;
(%o3)                   2.917363553109228
@end group
(%i4) s2 : read_matrix (file_search ("wind.data"))$
@group
(%i5) std1 (s2);
(%o5) [4.17083509672109, 3.89090320978032, 3.953738641137555, 
                            5.701010936401517, 4.966867617451963]
@end group
@end example

V@'eanse tambi@'en @code{var1} y @code{std}.
@end deffn



@deffn {Funci@'on} noncentral_moment (@var{list}, @var{k})
@deffnx {Funci@'on} noncentral_moment (@var{matrix}, @var{k})
Es el momento no central de orden @math{k}, definido como
@ifnottex
@example
                       n
                     ====
                 1   \      k
                 -    >    x
                 n   /      i
                     ====
                     i = 1
@end example
@end ifnottex
@tex
$${{1\over{n}}{\sum_{i=1}^{n}{x_{i}^k}}}$$
@end tex


Ejemplo:

@c ===beg===
@c load (descriptive)$
@c s1 : read_list (file_search ("pidigits.data"))$
@c noncentral_moment (s1, 1), numer; /* the mean */
@c s2 : read_matrix (file_search ("wind.data"))$
@c noncentral_moment (s2, 5);
@c ===end===
@example
(%i1) load (descriptive)$
(%i2) s1 : read_list (file_search ("pidigits.data"))$
@group
(%i3) noncentral_moment (s1, 1), numer; /* the mean */
(%o3)                         4.71
@end group
@group
(%i5) s2 : read_matrix (file_search ("wind.data"))$
(%o5) [319793.8724761506, 320532.1923892463, 391249.5621381556, 
                            2502278.205988911, 1691881.797742255]
@end group
(%i6) noncentral_moment (s2, 5);
@end example

V@'ease tambi@'en @code{central_moment}.
@end deffn



@deffn {Funci@'on} central_moment (@var{list}, @var{k})
@deffnx {Funci@'on} central_moment (@var{matrix}, @var{k})
Es el momento central de orden @math{k}, definido como
@ifnottex
@example
                    n
                  ====
              1   \          _ k
              -    >    (x - x)
              n   /       i
                  ====
                  i = 1
@end example
@end ifnottex
@tex
$${{1\over{n}}{\sum_{i=1}^{n}{(x_{i}-\bar{x})^k}}}$$
@end tex


Ejemplo:

@c ===beg===
@c load (descriptive)$
@c s1 : read_list (file_search ("pidigits.data"))$
@c central_moment (s1, 2), numer; /* the variance */
@c s2 : read_matrix (file_search ("wind.data"))$
@c central_moment (s2, 3);
@c ===end===
@example
(%i1) load (descriptive)$
(%i2) s1 : read_list (file_search ("pidigits.data"))$
@group
(%i3) central_moment (s1, 2), numer; /* the variance */
(%o3)                   8.425899999999999
@end group
@group
(%i5) s2 : read_matrix (file_search ("wind.data"))$
(%o5) [11.29584771375004, 16.97988248298583, 5.626661952750102, 
                             37.5986572057918, 25.85981904394192]
@end group
(%i6) central_moment (s2, 3);
@end example

V@'eanse tambi@'en @code{central_moment} y @code{mean}.
@end deffn


@deffn {Funci@'on} cv (@var{list})
@deffnx {Funci@'on} cv (@var{matrix})
Es el coeficiente de variaci@'on, o cociente entre la desviaci@'on t@'{@dotless{i}}pica muestral (@code{std}) y la media (@code{mean}),

@c ===beg===
@c load (descriptive)$
@c s1 : read_list (file_search ("pidigits.data"))$
@c cv (s1), numer;
@c s2 : read_matrix (file_search ("wind.data"))$
@c cv (s2);
@c ===end===
@example
(%i1) load (descriptive)$
(%i2) s1 : read_list (file_search ("pidigits.data"))$
@group
(%i3) cv (s1), numer;
(%o3)                   .6193977819764815
@end group
(%i4) s2 : read_matrix (file_search ("wind.data"))$
@group
(%i5) cv (s2);
(%o5) [.4192426091090204, .3829365309260502, 0.363779605385983, 
                            .3627381836021478, .3346021393989506]
@end group
@end example

V@'eanse tambi@'en @code{std} y @code{mean}.
@end deffn



@deffn {Funci@'on} smin (@var{list})
@deffnx {Funci@'on} smin (@var{matrix})
Es el valor m@'{@dotless{i}}nimo de la muestra @var{list}.
Cuando el argumento es una matriz, @code{smin} devuelve
una lista con los valores m@'{@dotless{i}}nimos de las
columnas, las cuales est@'an asociadas a variables
estad@'{@dotless{i}}sticas.

@c ===beg===
@c load (descriptive)$
@c s1 : read_list (file_search ("pidigits.data"))$
@c smin (s1);
@c s2 : read_matrix (file_search ("wind.data"))$
@c smin (s2);
@c ===end===
@example
(%i1) load (descriptive)$
(%i2) s1 : read_list (file_search ("pidigits.data"))$
@group
(%i3) smin (s1);
(%o3)                           0
@end group
(%i4) s2 : read_matrix (file_search ("wind.data"))$
@group
(%i5) smin (s2);
(%o5)             [0.58, 0.5, 2.67, 5.25, 5.17]
@end group
@end example

V@'ease tambi@'en @code{smax}.
@end deffn



@deffn {Funci@'on} smax (@var{list})
@deffnx {Funci@'on} smax (@var{matrix})
Es el valor m@'aximo de la muestra @var{list}.
Cuando el argumento es una matriz, @code{smax} devuelve
una lista con los valores m@'aximos de las
columnas, las cuales est@'an asociadas a variables
estad@'{@dotless{i}}sticas.

@c ===beg===
@c load (descriptive)$
@c s1 : read_list (file_search ("pidigits.data"))$
@c smax (s1);
@c s2 : read_matrix (file_search ("wind.data"))$
@c smax (s2);
@c ===end===
@example
(%i1) load (descriptive)$
(%i2) s1 : read_list (file_search ("pidigits.data"))$
@group
(%i3) smax (s1);
(%o3)                           9
@end group
(%i4) s2 : read_matrix (file_search ("wind.data"))$
@group
(%i5) smax (s2);
(%o5)          [20.25, 21.46, 20.04, 29.63, 27.63]
@end group
@end example

V@'ease tambi@'en @code{smin}.
@end deffn



@deffn {Funci@'on} range (@var{list})
@deffnx {Funci@'on} range (@var{matrix})
Es la diferencia entre los valores extremos.

Ejemplo:

@c ===beg===
@c load (descriptive)$
@c s1 : read_list (file_search ("pidigits.data"))$
@c range (s1);
@c s2 : read_matrix (file_search ("wind.data"))$
@c range (s2);
@c ===end===
@example
(%i1) load (descriptive)$
(%i2) s1 : read_list (file_search ("pidigits.data"))$
@group
(%i3) range (s1);
(%o3)                           9
@end group
(%i4) s2 : read_matrix (file_search ("wind.data"))$
@group
(%i5) range (s2);
(%o5)          [19.67, 20.96, 17.37, 24.38, 22.46]
@end group
@end example

@end deffn



@deffn {Funci@'on} quantile (@var{list}, @var{p})
@deffnx {Funci@'on} quantile (@var{matrix}, @var{p})
Es el @var{p}-cuantil, siendo @var{p} un n@'umero del intervalo @math{[0, 1]}, de la muestra @var{list}.
Aunque existen varias definiciones para el cuantil muestral (Hyndman, R. J., Fan, Y. (1996) @var{Sample quantiles in statistical packages}. American Statistician, 50, 361-365), la programada en el paquete  @code{descriptive} es la basada en la interpolaci@'on lineal.

Ejemplo:

@c ===beg===
@c load (descriptive)$
@c s1 : read_list (file_search ("pidigits.data"))$
@c /* 1st and 3rd quartiles */ 
@c          [quantile (s1, 1/4), quantile (s1, 3/4)], numer;
@c s2 : read_matrix (file_search ("wind.data"))$
@c quantile (s2, 1/4);
@c ===end===
@example
(%i1) load (descriptive)$
(%i2) s1 : read_list (file_search ("pidigits.data"))$
@group
(%i3) /* 1st and 3rd quartiles */
         [quantile (s1, 1/4), quantile (s1, 3/4)], numer;
(%o3)                      [2.0, 7.25]
@end group
(%i4) s2 : read_matrix (file_search ("wind.data"))$
@group
(%i5) quantile (s2, 1/4);
(%o5)    [7.2575, 7.477500000000001, 7.82, 11.28, 11.48]
@end group
@end example

@end deffn



@deffn {Funci@'on} median (@var{list})
@deffnx {Funci@'on} median (@var{matrix})
Una vez ordenada una muestra, si el tama@~no muestral es impar la mediana es el valor central, en caso contrario ser@'a la media de los dos valores centrales.

Ejemplo:

@c ===beg===
@c load (descriptive)$
@c s1 : read_list (file_search ("pidigits.data"))$
@c median (s1);
@c s2 : read_matrix (file_search ("wind.data"))$
@c median (s2);
@c ===end===
@example
(%i1) load (descriptive)$
(%i2) s1 : read_list (file_search ("pidigits.data"))$
@group
(%i3) median (s1);
                                9
(%o3)                           -
                                2
@end group
(%i4) s2 : read_matrix (file_search ("wind.data"))$
@group
(%i5) median (s2);
(%o5)         [10.06, 9.855, 10.73, 15.48, 14.105]
@end group
@end example

La mediana es el cuantil 1/2.

V@'ease tambi@'en @code{quantile}.
@end deffn



@deffn {Funci@'on} qrange (@var{list})
@deffnx {Funci@'on} qrange (@var{matrix})
El rango intercuart@'{@dotless{i}}lico es la diferencia entre el tercer y primer cuartil,
@code{quantile(@var{list},3/4) - quantile(@var{list},1/4)},

@c ===beg===
@c load (descriptive)$
@c s1 : read_list (file_search ("pidigits.data"))$
@c qrange (s1);
@c s2 : read_matrix (file_search ("wind.data"))$
@c qrange (s2);
@c ===end===
@example
(%i1) load (descriptive)$
(%i2) s1 : read_list (file_search ("pidigits.data"))$
@group
(%i3) qrange (s1);
                               21
(%o3)                          --
                               4
@end group
(%i4) s2 : read_matrix (file_search ("wind.data"))$
@group
(%i5) qrange (s2);
(%o5) [5.385, 5.572499999999998, 6.0225, 8.729999999999999, 
                                               6.650000000000002]
@end group
@end example

V@'ease tambi@'en @code{quantile}.
@end deffn



@deffn {Funci@'on} mean_deviation (@var{list})
@deffnx {Funci@'on} mean_deviation (@var{matrix})
Es la desviaci@'on media, definida como
@ifnottex
@example
                     n
                   ====
               1   \          _
               -    >    |x - x|
               n   /       i
                   ====
                   i = 1
@end example
@end ifnottex
@tex
$${{1\over{n}}{\sum_{i=1}^{n}{|x_{i}-\bar{x}|}}}$$
@end tex


Ejemplo:

@c ===beg===
@c load (descriptive)$
@c s1 : read_list (file_search ("pidigits.data"))$
@c mean_deviation (s1);
@c s2 : read_matrix (file_search ("wind.data"))$
@c mean_deviation (s2);
@c ===end===
@example
(%i1) load (descriptive)$
(%i2) s1 : read_list (file_search ("pidigits.data"))$
@group
(%i3) mean_deviation (s1);
                               51
(%o3)                          --
                               20
@end group
(%i4) s2 : read_matrix (file_search ("wind.data"))$
@group
(%i5) mean_deviation (s2);
(%o5) [3.287959999999999, 3.075342, 3.23907, 4.715664000000001, 
                                               4.028546000000002]
@end group
@end example

V@'ease tambi@'en @code{mean}.
@end deffn



@deffn {Funci@'on} median_deviation (@var{list})
@deffnx {Funci@'on} median_deviation (@var{matrix})
Es la desviaci@'on mediana, definida como
@ifnottex
@example
                 n
               ====
           1   \
           -    >    |x - med|
           n   /       i
               ====
               i = 1
@end example
@end ifnottex
@tex
$${{1\over{n}}{\sum_{i=1}^{n}{|x_{i}-med|}}}$$
@end tex

siendo @code{med} la mediana de @var{list}.

Ejemplo:

@c ===beg===
@c load (descriptive)$
@c s1 : read_list (file_search ("pidigits.data"))$
@c median_deviation (s1);
@c s2 : read_matrix (file_search ("wind.data"))$
@c median_deviation (s2);
@c ===end===
@example
(%i1) load (descriptive)$
(%i2) s1 : read_list (file_search ("pidigits.data"))$
@group
(%i3) median_deviation (s1);
                                5
(%o3)                           -
                                2
@end group
(%i4) s2 : read_matrix (file_search ("wind.data"))$
@group
(%i5) median_deviation (s2);
(%o5)           [2.75, 2.755, 3.08, 4.315, 3.31]
@end group
@end example

V@'ease tambi@'en @code{mean}.
@end deffn



@deffn {Funci@'on} harmonic_mean (@var{list})
@deffnx {Funci@'on} harmonic_mean (@var{matrix})
Es la media arm@'onica, definida como
@ifnottex
@example
                  n
               --------
                n
               ====
               \     1
                >    --
               /     x
               ====   i
               i = 1
@end example
@end ifnottex
@tex
$${{n}\over{\sum_{i=1}^{n}{{{1}\over{x_{i}}}}}}$$
@end tex


Ejemplo:

@c ===beg===
@c load (descriptive)$
@c y : [5, 7, 2, 5, 9, 5, 6, 4, 9, 2, 4, 2, 5]$
@c harmonic_mean (y), numer;
@c s2 : read_matrix (file_search ("wind.data"))$
@c harmonic_mean (s2);
@c ===end===
@example
(%i1) load (descriptive)$
(%i2) y : [5, 7, 2, 5, 9, 5, 6, 4, 9, 2, 4, 2, 5]$
@group
(%i3) harmonic_mean (y), numer;
(%o3)                   3.901858027632205
@end group
(%i4) s2 : read_matrix (file_search ("wind.data"))$
@group
(%i5) harmonic_mean (s2);
(%o5) [6.948015590052786, 7.391967752360356, 9.055658197151745, 
                            13.44199028193692, 13.01439145898509]
@end group
@end example

V@'eanse tambi@'en @code{mean} y @code{geometric_mean}.
@end deffn



@deffn {Funci@'on} geometric_mean (@var{list})
@deffnx {Funci@'on} geometric_mean (@var{matrix})
Es la media geom@'etrica, definida como
@ifnottex
@example
                 /  n      \ 1/n
                 | /===\   |
                 |  ! !    |
                 |  ! !  x |
                 |  ! !   i|
                 | i = 1   |
                 \         /
@end example
@end ifnottex
@tex
$$\left(\prod_{i=1}^{n}{x_{i}}\right)^{{{1}\over{n}}}$$
@end tex


Ejemplo:

@c ===beg===
@c load (descriptive)$
@c y : [5, 7, 2, 5, 9, 5, 6, 4, 9, 2, 4, 2, 5]$
@c geometric_mean (y), numer;
@c s2 : read_matrix (file_search ("wind.data"))$
@c geometric_mean (s2);
@c ===end===
@example
(%i1) load (descriptive)$
(%i2) y : [5, 7, 2, 5, 9, 5, 6, 4, 9, 2, 4, 2, 5]$
@group
(%i3) geometric_mean (y), numer;
(%o3)                   4.454845412337012
@end group
(%i4) s2 : read_matrix (file_search ("wind.data"))$
@group
(%i5) geometric_mean (s2);
(%o5) [8.82476274347979, 9.22652604739361, 10.0442675714889, 
                            14.61274126349021, 13.96184163444275]
@end group
@end example

V@'eanse tambi@'en @code{mean} y @code{harmonic_mean}.
@end deffn



@deffn {Funci@'on} kurtosis (@var{list})
@deffnx {Funci@'on} kurtosis (@var{matrix})
Es el coeficiente de curtosis, definido como
@ifnottex
@example
                    n
                  ====
            1     \          _ 4
           ----    >    (x - x)  - 3
              4   /       i
           n s    ====
                  i = 1
@end example
@end ifnottex
@tex
$${{1\over{n s^4}}{\sum_{i=1}^{n}{(x_{i}-\bar{x})^4}}-3}$$
@end tex


Ejemplo:

@c ===beg===
@c load (descriptive)$
@c s1 : read_list (file_search ("pidigits.data"))$
@c kurtosis (s1), numer;
@c s2 : read_matrix (file_search ("wind.data"))$
@c kurtosis (s2);
@c ===end===
@example
(%i1) load (descriptive)$
(%i2) s1 : read_list (file_search ("pidigits.data"))$
@group
(%i3) kurtosis (s1), numer;
(%o3)                  - 1.273247946514421
@end group
(%i4) s2 : read_matrix (file_search ("wind.data"))$
@group
(%i5) kurtosis (s2);
(%o5) [- .2715445622195385, 0.119998784429451, 
   - .4275233490482866, - .6405361979019522, - .4952382132352935]
@end group
@end example

V@'eanse tambi@'en @code{mean}, @code{var} y @code{skewness}.
@end deffn



@deffn {Funci@'on} skewness (@var{list})
@deffnx {Funci@'on} skewness (@var{matrix})
Es el coeficiente de asimetr@'{@dotless{i}}a, definido como
@ifnottex
@example
                    n
                  ====
            1     \          _ 3
           ----    >    (x - x)
              3   /       i
           n s    ====
                  i = 1
@end example
@end ifnottex
@tex
$${{1\over{n s^3}}{\sum_{i=1}^{n}{(x_{i}-\bar{x})^3}}}$$
@end tex


Ejemplo:

@c ===beg===
@c load (descriptive)$
@c s1 : read_list (file_search ("pidigits.data"))$
@c skewness (s1), numer;
@c s2 : read_matrix (file_search ("wind.data"))$
@c skewness (s2);
@c ===end===
@example
(%i1) load (descriptive)$
(%i2) s1 : read_list (file_search ("pidigits.data"))$
@group
(%i3) skewness (s1), numer;
(%o3)                  .009196180476450306
@end group
(%i4) s2 : read_matrix (file_search ("wind.data"))$
@group
(%i5) skewness (s2);
(%o5) [.1580509020000979, .2926379232061854, .09242174416107717, 
                            .2059984348148687, .2142520248890832]
@end group
@end example

V@'eanse tambi@'en @code{mean}, @code{var} y @code{kurtosis}.
@end deffn



@deffn {Funci@'on} pearson_skewness (@var{list})
@deffnx {Funci@'on} pearson_skewness (@var{matrix})
Es el coeficiente de asimetr@'{@dotless{i}}a de Pearson, definido como
@ifnottex
@example
                _
             3 (x - med)
             -----------
                  s
@end example
@end ifnottex
@tex
$${{3\,\left(\bar{x}-med\right)}\over{s}}$$
@end tex

siendo @var{med} la mediana de @var{list}.

Ejemplo:

@c ===beg===
@c load (descriptive)$
@c s1 : read_list (file_search ("pidigits.data"))$
@c pearson_skewness (s1), numer;
@c s2 : read_matrix (file_search ("wind.data"))$
@c pearson_skewness (s2);
@c ===end===
@example
(%i1) load (descriptive)$
(%i2) s1 : read_list (file_search ("pidigits.data"))$
@group
(%i3) pearson_skewness (s1), numer;
(%o3)                   .2159484029093895
@end group
(%i4) s2 : read_matrix (file_search ("wind.data"))$
@group
(%i5) pearson_skewness (s2);
(%o5) [- .08019976629211892, .2357036272952649, 
         .1050904062491204, .1245042340592368, .4464181795804519]
@end group
@end example

V@'eanse tambi@'en @code{mean}, @code{var} y @code{median}.
@end deffn



@deffn {Funci@'on} quartile_skewness (@var{list})
@deffnx {Funci@'on} quartile_skewness (@var{matrix})
Es el coeficiente de asimetr@'{@dotless{i}}a cuart@'{@dotless{i}}lico, definido como
@ifnottex
@example
               c    - 2 c    + c
                3/4      1/2    1/4
               --------------------
                   c    - c
                    3/4    1/4
@end example
@end ifnottex
@tex
$${{c_{{{3}\over{4}}}-2\,c_{{{1}\over{2}}}+c_{{{1}\over{4}}}}\over{c
 _{{{3}\over{4}}}-c_{{{1}\over{4}}}}}$$
@end tex

siendo @math{c_p} el @var{p}-cuantil de la muestra @var{list}.

Ejemplo:

@c ===beg===
@c load (descriptive)$
@c s1 : read_list (file_search ("pidigits.data"))$
@c quartile_skewness (s1), numer;
@c s2 : read_matrix (file_search ("wind.data"))$
@c quartile_skewness (s2);
@c ===end===
@example
(%i1) load (descriptive)$
(%i2) s1 : read_list (file_search ("pidigits.data"))$
@group
(%i3) quartile_skewness (s1), numer;
(%o3)                  .04761904761904762
@end group
(%i4) s2 : read_matrix (file_search ("wind.data"))$
@group
(%i5) quartile_skewness (s2);
(%o5) [- 0.0408542246982353, .1467025572005382, 
       0.0336239103362392, .03780068728522298, 0.210526315789474]
@end group
@end example

V@'ease tambi@'en @code{quantile}.
@end deffn




@deffn {Funci@'on} cov (@var{matrix})
Es la matriz de covarianzas de una muestra multivariante, definida como
@ifnottex
@example
              n
             ====
          1  \           _        _
      S = -   >    (X  - X) (X  - X)'
          n  /       j        j
             ====
             j = 1
@end example
@end ifnottex
@tex
$${S={1\over{n}}{\sum_{j=1}^{n}{\left(X_{j}-\bar{X}\right)\,\left(X_{j}-\bar{X}\right)'}}}$$
@end tex

siendo @math{X_j} la @math{j}-@'esima fila de la matriz muestral.

Ejemplo:

@c ===beg===
@c load (descriptive)$
@c s2 : read_matrix (file_search ("wind.data"))$
@c fpprintprec : 7$  /* change precision for pretty output */
@c cov (s2);
@c ===end===
@example
(%i1) load (descriptive)$
(%i2) s2 : read_matrix (file_search ("wind.data"))$
(%i3) fpprintprec : 7$  /* change precision for pretty output */
      [ 17.22191  13.61811  14.37217  19.39624  15.42162 ]
      [                                                  ]
      [ 13.61811  14.98774  13.30448  15.15834  14.9711  ]
      [                                                  ]
(%o4) [ 14.37217  13.30448  15.47573  17.32544  16.18171 ]
      [                                                  ]
      [ 19.39624  15.15834  17.32544  32.17651  20.44685 ]
      [                                                  ]
      [ 15.42162  14.9711   16.18171  20.44685  24.42308 ]
(%i5) cov (s2);
@end example

V@'ease tambi@'en @code{cov1}.
@end deffn



@deffn {Funci@'on} cov1 (@var{matrix})
Es la matriz de cuasivarianzas de una muestra multivariante, definida como
@ifnottex
@example
              n
             ====
         1   \           _        _
   S  = ---   >    (X  - X) (X  - X)'
    1   n-1  /       j        j
             ====
             j = 1
@end example
@end ifnottex
@tex
$${{1\over{n-1}}{\sum_{j=1}^{n}{\left(X_{j}-\bar{X}\right)\,\left(X_{j}-\bar{X}\right)'}}}$$
@end tex

siendo @math{X_j} la @math{j}-@'esima fila de la matriz muestral.

Ejemplo:

@c ===beg===
@c load (descriptive)$
@c s2 : read_matrix (file_search ("wind.data"))$
@c fpprintprec : 7$ /* change precision for pretty output */
@c cov1 (s2);
@c ===end===
@example
(%i1) load (descriptive)$
(%i2) s2 : read_matrix (file_search ("wind.data"))$
(%i3) fpprintprec : 7$ /* change precision for pretty output */
      [ 17.39587  13.75567  14.51734  19.59216  15.5774  ]
      [                                                  ]
      [ 13.75567  15.13913  13.43887  15.31145  15.12232 ]
      [                                                  ]
(%o4) [ 14.51734  13.43887  15.63205  17.50044  16.34516 ]
      [                                                  ]
      [ 19.59216  15.31145  17.50044  32.50153  20.65338 ]
      [                                                  ]
      [ 15.5774   15.12232  16.34516  20.65338  24.66977 ]
(%i5) cov1 (s2);
@end example

V@'ease tambi@'en @code{cov}.
@end deffn



@deffn {Funci@'on} global_variances (@var{matrix})
@deffnx {Funci@'on} global_variances (@var{matrix}, @var{options} ...)
La funci@'on @code{global_variances} devuelve una lista de medidas globales de variabilidad:

@itemize @bullet
@item
@var{varianza total}: @code{trace(S_1)},
@item
@var{varianza media}: @code{trace(S_1)/p},
@item
@var{varianza generalizada}: @code{determinant(S_1)},
@item
@var{desviaci@'on t@'{@dotless{i}}pica generalizada}: @code{sqrt(determinant(S_1))},
@item
@var{varianza efectiva} @code{determinant(S_1)^(1/p)}, (definida en: Pe@~na, D. (2002) @var{An@'alisis de datos multivariantes}; McGraw-Hill, Madrid.)
@item
@var{desviaci@'on t@'{@dotless{i}}pica efectiva}: @code{determinant(S_1)^(1/(2*p))}.
@end itemize
donde @var{p} es la dimensi@'on de la variable aleatoria multivariante y @math{S_1} la matriz de covarianzas devuelta por la funci@'on @code{cov1}.

Opci@'on:

@itemize @bullet
@item
@code{'data}, por defecto @code{'true}, indica si la matriz de entrada contiene los datos muestrales,
en cuyo caso la matriz de covarianzas @code{cov1} debe ser calculada; en caso contrario, se le debe
pasar @'esta a la funci@'on como matriz sim@'etrica en lugar de los datos.
@end itemize

Ejemplo:

@c ===beg===
@c load (descriptive)$
@c s2 : read_matrix (file_search ("wind.data"))$
@c global_variances (s2);
@c ===end===
@example
(%i1) load (descriptive)$
(%i2) s2 : read_matrix (file_search ("wind.data"))$
@group
(%i3) global_variances (s2);
(%o3) [105.338342060606, 21.06766841212119, 12874.34690469686, 
         113.4651792608502, 6.636590811800794, 2.576158149609762]
@end group
@end example

C@'alculo de @code{global_variances} a partir de la matriz de covarianzas.

@c ===beg===
@c load (descriptive)$
@c s2 : read_matrix (file_search ("wind.data"))$
@c s : cov1 (s2)$
@c global_variances (s, data=false);
@c ===end===
@example
(%i1) load (descriptive)$
(%i2) s2 : read_matrix (file_search ("wind.data"))$
(%i3) s : cov1 (s2)$
@group
(%i4) global_variances (s, data=false);
(%o4) [105.338342060606, 21.06766841212119, 12874.34690469686, 
         113.4651792608502, 6.636590811800794, 2.576158149609762]
@end group
@end example

V@'eanse tambi@'en @code{cov} y @code{cov1}.
@end deffn



@deffn {Funci@'on} cor (@var{matrix})
@deffnx {Funci@'on} cor (@var{matrix}, @var{options} ...)
Es la matriz de correlaciones de la muestra multivariante.

Opci@'on:

@itemize @bullet
@item
@code{'data}, por defecto @code{'true}, indica si la matriz de entrada contiene los datos muestrales,
en cuyo caso la matriz de covarianzas @code{cov1} debe ser calculada; en caso contrario, se le debe
pasar @'esta a la funci@'on como matriz sim@'etrica en lugar de los datos.
@end itemize

Ejemplo:

@c ===beg===
@c load (descriptive)$
@c fpprintprec:7$
@c s2 : read_matrix (file_search ("wind.data"))$
@c cor (s2);
@c ===end===
@example
(%i1) load (descriptive)$
(%i2) fpprintprec:7$
(%i3) s2 : read_matrix (file_search ("wind.data"))$
(%i4) cor (s2);
      [   1.0     .8476339  .8803515  .8239624  .7519506 ]
      [                                                  ]
      [ .8476339    1.0     .8735834  .6902622  0.782502 ]
      [                                                  ]
(%o4) [ .8803515  .8735834    1.0     .7764065  .8323358 ]
      [                                                  ]
      [ .8239624  .6902622  .7764065    1.0     .7293848 ]
      [                                                  ]
      [ .7519506  0.782502  .8323358  .7293848    1.0    ]
@end example

C@'alculo de la matriz de correlaciones a partir de la matriz de covarianzas.

@c ===beg===
@c load (descriptive)$
@c fpprintprec : 7 $
@c s2 : read_matrix (file_search ("wind.data"))$
@c s : cov1 (s2)$
@c cor (s, data=false); /* this is faster */
@c ===end===
@example
(%i1) load (descriptive)$
(%i2) fpprintprec : 7 $
(%i3) s2 : read_matrix (file_search ("wind.data"))$
(%i4) s : cov1 (s2)$
@group
(%i5) cor (s, data=false); /* this is faster */
      [   1.0     .8476339  .8803515  .8239624  .7519506 ]
      [                                                  ]
      [ .8476339    1.0     .8735834  .6902622  0.782502 ]
      [                                                  ]
(%o5) [ .8803515  .8735834    1.0     .7764065  .8323358 ]
      [                                                  ]
      [ .8239624  .6902622  .7764065    1.0     .7293848 ]
      [                                                  ]
      [ .7519506  0.782502  .8323358  .7293848    1.0    ]
@end group
@end example

V@'eanse tambi@'en @code{cov} y @code{cov1}.
@end deffn



@deffn {Funci@'on} list_correlations (@var{matrix})
@deffnx {Funci@'on} list_correlations (@var{matrix}, @var{options} ...)
La funci@'on @code{list_correlations} devuelve una lista con medidas de correlaci@'on:

@itemize @bullet

@item
@var{matriz de precisi@'on}: es la inversa de la matriz de covarianzas @math{S_1},
@ifnottex
@example
       -1     ij
      S   = (s  )             
       1         i,j = 1,2,...,p
@end example
@end ifnottex
@tex
$${S_{1}^{-1}}={\left(s^{ij}\right)_{i,j=1,2,\ldots, p}}$$
@end tex

@item
@var{multiple correlation vector}:  @math{(R_1^2, R_2^2, ..., R_p^2)}, with 
@ifnottex
@example
       2          1
      R  = 1 - -------
       i        ii
               s   s
                    ii
@end example
@end ifnottex
@tex
$${R_{i}^{2}}={1-{{1}\over{s^{ii}s_{ii}}}}$$
@end tex
es un indicador de la bondad de ajuste del modelo de regresi@'on lineal multivariante de @math{X_i} cuando el resto de variables se utilizan como regresores.

@item
@var{matriz de correlaciones parciales}: en la que el elemento @math{(i, j)} es
@ifnottex
@example
                         ij
                        s
      r        = - ------------
       ij.rest     / ii  jj\ 1/2
                   |s   s  |
                   \       /
@end example
@end ifnottex
@tex
$${r_{ij.rest}}={-{{s^{ij}}\over \sqrt{s^{ii}s^{jj}}}}$$
@end tex
@end itemize

Opci@'on:

@itemize @bullet
@item
@code{'data}, por defecto @code{'true}, indica si la matriz de entrada contiene los datos muestrales,
en cuyo caso la matriz de covarianzas @code{cov1} debe ser calculada; en caso contrario, se le debe
pasar @'esta a la funci@'on como matriz sim@'etrica en lugar de los datos.
@end itemize

Ejemplo:

@c ===beg===
@c load (descriptive)$
@c s2 : read_matrix (file_search ("wind.data"))$
@c z : list_correlations (s2)$
@c fpprintprec : 5$ /* for pretty output */
@c z[1];  /* precision matrix */
@c z[2];  /* multiple correlation vector */
@c z[3];  /* partial correlation matrix */
@c ===end===
@example
(%i1) load (descriptive)$
(%i2) s2 : read_matrix (file_search ("wind.data"))$
(%i3) z : list_correlations (s2)$
@group
(%i4) fpprintprec : 5$ /* for pretty output */
      [  .38486   - .13856   - .15626   - .10239    .031179  ]
      [                                                      ]
      [ - .13856   .34107    - .15233    .038447   - .052842 ]
      [                                                      ]
(%o5) [ - .15626  - .15233    .47296    - .024816  - .10054  ]
      [                                                      ]
      [ - .10239   .038447   - .024816   .10937    - .034033 ]
      [                                                      ]
      [ .031179   - .052842  - .10054   - .034033   .14834   ]
(%o6)       [.85063, .80634, .86474, .71867, .72675]
       [  - 1.0     .38244   .36627   .49908   - .13049 ]
       [                                                ]
       [  .38244    - 1.0    .37927  - .19907   .23492  ]
       [                                                ]
(%o7)  [  .36627    .37927   - 1.0    .10911    .37956  ]
       [                                                ]
       [  .49908   - .19907  .10911   - 1.0     .26719  ]
       [                                                ]
       [ - .13049   .23492   .37956   .26719    - 1.0   ]
@end group
@end example

V@'eanse tambi@'en @code{cov} y @code{cov1}.
@end deffn



@deffn {Funci@'on} principal_components (@var{matrix})
@deffnx {Funci@'on} principal_components (@var{matrix}, @var{options} ...)
Calcula las componentes principales de una muestra multivariante. Las componentes
principales se utilizan en el an@'alisis estad@'{@dotless{i}}stico multivariante
para reducir la dimensionalidad de la muestra.

Opci@'on:

@itemize @bullet
@item
@code{'data}, por defecto @code{'true}, indica si la matriz de entrada contiene los datos muestrales,
en cuyo caso la matriz de covarianzas @code{cov1} debe ser calculada; en caso contrario, se le debe
pasar @'esta a la funci@'on como matriz sim@'etrica en lugar de los datos.
@end itemize

La salida de la funci@'on @code{principal_components} es una lista con los siguientes resultados:

@itemize @bullet
@item
varianzas de las componentes principales,
@item
porcentajes de variaci@'on total explicada por cada componente principal,
@item
matriz de rotaci@'on.
@end itemize

Ejemplos:

En este ejemplo, la primera componente explica el 83.13 por ciento de la varianza total.

@example
(%i1) load (descriptive)$
(%i2) s2 : read_matrix (file_search ("wind.data"))$
(%i3) fpprintprec:4 $
(%i4) res: principal_components(s2);
0 errors, 0 warnings
(%o4) [[87.57, 8.753, 5.515, 1.889, 1.613], 
[83.13, 8.31, 5.235, 1.793, 1.531], 
@group
[ .4149  .03379   - .4757  - 0.581   - .5126 ]
[                                            ]
[ 0.369  - .3657  - .4298   .7237    - .1469 ]
[                                            ]
[ .3959  - .2178  - .2181  - .2749    .8201  ]]
[                                            ]
[ .5548   .7744    .1857    .2319    .06498  ]
[                                            ]
[ .4765  - .4669   0.712   - .09605  - .1969 ]
@end group
(%i5) /* porcentajes acumulados  */
    block([ap: copy(res[2])],
      for k:2 thru length(ap) do ap[k]: ap[k]+ap[k-1],
      ap);
(%o5)                 [83.13, 91.44, 96.68, 98.47, 100.0]
(%i6) /* dimension de la muestra */
      p: length(first(res));
(%o6)                                  5
(%i7) /* dibuja porcentajes para seleccionar el numero de 
         componentes principales para el analisis ulterior */
     draw2d(
        fill_density = 0.2,
        apply(bars, makelist([k, res[2][k], 1/2], k, p)),
        points_joined = true,
        point_type    = filled_circle,
        point_size    = 3,
        points(makelist([k, res[2][k]], k, p)),
        xlabel = "Variances",
        ylabel = "Percentages",
        xtics  = setify(makelist([concat("PC",k),k], k, p))) $
@end example

En caso de que la matriz de covarianzas sea conocida, se le puede pasar
a la funci@'on, pero debe utilizarse la opci@'on @code{data=false}.

@example
(%i1) load (descriptive)$
(%i2) S: matrix([1,-2,0],[-2,5,0],[0,0,2]);
                                [  1   - 2  0 ]
                                [             ]
(%o2)                           [ - 2   5   0 ]
                                [             ]
                                [  0    0   2 ]
(%i3) fpprintprec:4 $
(%i4) /* el argumento es una matriz de covarianzas */
      res: principal_components(S, data=false);
0 errors, 0 warnings
                                                  [ - .3827  0.0  .9239 ]
                                                  [                     ]
(%o4) [[5.828, 2.0, .1716], [72.86, 25.0, 2.145], [  .9239   0.0  .3827 ]]
                                                  [                     ]
                                                  [   0.0    1.0   0.0  ]
(%i5) /* transformacion para obtener las componentes principales a
         partir de los registros originales */
      matrix([a1,b2,c3],[a2,b2,c2]).last(res);
             [ .9239 b2 - .3827 a1  1.0 c3  .3827 b2 + .9239 a1 ]
(%o5)        [                                                  ]
             [ .9239 b2 - .3827 a2  1.0 c2  .3827 b2 + .9239 a2 ]
@end example

@end deffn





@node Funciones y variables para gr@'aficos estad@'{@dotless{i}}sticos,  , Funciones y variables de par@'ametros descriptivos, descriptive
@section Funciones y variables para gr@'aficos estad@'{@dotless{i}}sticos


@deffn {Funci@'on} barsplot (@var{data1}, @var{data2}, @dots{}, @var{option_1}, @var{option_2}, @dots{})
@deffnx {Funci@'on} barsplot_description (@dots{})

Dibuja diagramas de barras para variables estad@'{@dotless{i}}sticas
discretas, tanto para una como para m@'as muestras.

@var{data} puede ser una lista de resultados provenientes de una muestra
o una matriz de @var{m} filas y @var{n} columnas, representando @var{n} 
muestras de tama@~no @var{m} cada una.

Las opciones disponibles son:

@itemize @bullet

@var{box_width} (valor por defecto, @code{3/4}): ancho relativo de los rect@'angulos. Este 
valor debe pertenecer al rango @code{[0,1]}.

@item
@var{grouping} (valor por defecto, @code{clustered}): indica c@'omo se agrupan las 
diferentes muestras. Son valores v@'alidos: @code{clustered} y @code{stacked}.

@item
@var{groups_gap} (valor por defecto, @code{1}): un n@'umero positivo que representa
la separaci@'on entre dos grupos consecutivos de barras.

@item
@var{bars_colors} (valor por defecto, @code{[]}): una lista de colores para m@'ultiples 
muestras. Cuando el n@'umero de muestras sea mayor que el de colores especificados, 
los colores adicionales necesarios se seleccionan aleatoriamente. V@'ease @code{color} para m@'as
informaci@'on.

@item
@var{frequency} (valor por defecto, @code{absolute}): indica la escala de las ordenadas.
Valores admitidos son:  @code{absolute}, @code{relative} y and @code{percent}.

@item
@var{ordering} (valor por defecto, @code{orderlessp}): los valores admitidos para esta 
opci@'on son: @code{orderlessp} y @code{ordergreatp}, indicando c@'omo se deben ordenar
los resultados muestrales sobre el eje-@var{x}.

@item
@var{sample_keys} (valor por defecto, @code{[]}): es una lista de cadenas de 
texto a usar como leyendas. Cuando la lista tenga una longitud diferente de cero o 
del n@'umero de muestras, se devolver@'a un mensaje de error.

@item
@var{start_at} (valor por defecto, @code{0}): indica a qu@'e altura comienza 
a dibujarse el gr@'afico de barra sobre el eje de abscisas.

@item
Todas las opciones globales de @code{draw}, excepto @code{xtics}, que se asigna
internamente por @code{barsplot}. Si es necesario que el usuario le d@'e su propio
valor a esta opci@'on, o quiere construir una escena m@'as compleja,
debe hacer uso de @code{barsplot_description}. V@'ease el ejemplo m@'as abajo.

@item
Las siguientes opciones locales de @code{draw}: @code{key}, @code{color}, 
@code{fill_color}, @code{fill_density} y @code{line_width}. V@'ease tambi@'en
@code{bars}.

@end itemize

La funci@'on @code{barsplot_description} crea un objeto gr@'afico
@'util para formar escenas complejas, junto con otros objetos gr@'aficos.
Se dispone tambi@'en de la funci@'on @code{wxbarsplot} para crear
histogramas incorporados en los interfaces wxMaxima y iMaxima.

Ejemplos:

Muestra univariante en formato matricial. Frecuencias absolutas.

@c ===beg===
@c load (descriptive)$
@c m : read_matrix (file_search ("biomed.data"))$
@c barsplot(
@c   col(m,2),
@c   title        = "Ages",
@c   xlabel       = "years",
@c   box_width    = 1/2,
@c   fill_density = 3/4)$
@c ===end===
@example
(%i1) load (descriptive)$
(%i2) m : read_matrix (file_search ("biomed.data"))$
@group
(%i3) barsplot(
        col(m,2),
        title        = "Ages",
        xlabel       = "years",
        box_width    = 1/2,
        fill_density = 3/4)$
@end group
@end example

Dos muestras de diferente tama@~no, con frecuencias
relativas y colores definidos por el usuario.

@c ===beg===
@c load (descriptive)$
@c l1:makelist(random(10),k,1,50)$
@c l2:makelist(random(10),k,1,100)$
@c barsplot(
@c    l1,l2,
@c    box_width = 1,
@c    fill_density = 1,
@c    bars_colors = [black, grey],
@c    frequency = relative,
@c    sample_keys = ["A", "B"])$
@c ===end===
@example
(%i1) load (descriptive)$
(%i2) l1:makelist(random(10),k,1,50)$
(%i3) l2:makelist(random(10),k,1,100)$
@group
(%i4) barsplot(
        l1,l2,
        box_width    = 1,
        fill_density = 1,
        bars_colors  = [black, grey],
        frequency = relative,
        sample_keys = ["A", "B"])$
@end group
@end example

Cuatro muestras no num@'ericas de igual tama@~no.

@c ===beg===
@c load (descriptive)$
@c barsplot(
@c   makelist([Yes, No, Maybe][random(3)+1],k,1,50),
@c   makelist([Yes, No, Maybe][random(3)+1],k,1,50),
@c   makelist([Yes, No, Maybe][random(3)+1],k,1,50),
@c   makelist([Yes, No, Maybe][random(3)+1],k,1,50),
@c   title      = "Asking for something to four groups",
@c   ylabel     = "# of individuals",
@c   groups_gap = 3,
@c   fill_density = 0.5,
@c   ordering = ordergreatp)$
@c ===end===
@example
(%i1) load (descriptive)$
@group
(%i2) barsplot(
        makelist([Yes, No, Maybe][random(3)+1],k,1,50),
        makelist([Yes, No, Maybe][random(3)+1],k,1,50),
        makelist([Yes, No, Maybe][random(3)+1],k,1,50),
        makelist([Yes, No, Maybe][random(3)+1],k,1,50),
        title  = "Asking for something to four groups",
        ylabel = "# of individuals",
        groups_gap   = 3,
        fill_density = 0.5,
        ordering     = ordergreatp)$
@end group
@end example

Barras apiladas verticalmente.

@c ===beg===
@c load (descriptive)$
@c barsplot(
@c   makelist([Yes, No, Maybe][random(3)+1],k,1,50),
@c   makelist([Yes, No, Maybe][random(3)+1],k,1,50),
@c   makelist([Yes, No, Maybe][random(3)+1],k,1,50),
@c   makelist([Yes, No, Maybe][random(3)+1],k,1,50),
@c   title      = "Asking for something to four groups",
@c   ylabel     = "# of individuals",
@c   grouping   = stacked,,
@c   fill_density = 0.5,
@c   ordering = ordergreatp)$
@c ===end===
@example
(%i1) load (descriptive)$
@group
(%i2) barsplot(
        makelist([Yes, No, Maybe][random(3)+1],k,1,50),
        makelist([Yes, No, Maybe][random(3)+1],k,1,50),
        makelist([Yes, No, Maybe][random(3)+1],k,1,50),
        makelist([Yes, No, Maybe][random(3)+1],k,1,50),
        title  = "Asking for something to four groups",
        ylabel = "# of individuals",
        grouping     = stacked,,
        fill_density = 0.5,
        ordering     = ordergreatp)$
@end group
@end example

@code{barsplot} en un contexto multiplot.

@example
(%i1) load (descriptive)$
(%i2) l1:makelist(random(10),k,1,50)$
(%i3) l2:makelist(random(10),k,1,100)$
(%i4) bp1 : 
        barsplot_description(
         l1,
         box_width = 1,
         fill_density = 0.5,
         bars_colors = [blue],
         frequency = relative)$
(%i5) bp2 : 
        barsplot_description(
         l2,
         box_width = 1,
         fill_density = 0.5,
         bars_colors = [red],
         frequency = relative)$
(%i6) draw(gr2d(bp1), gr2d(bp2))$
@end example

Para las opciones relacionadas con los diagramas de barras, v@'ease @code{bars}
del paquete @code{draw}.

V@'eanse tambi@'en las funciones @code{histogram} y @code{piechart}.
@end deffn



@deffn {Funci@'on} boxplot (@var{data})
@deffnx {Funci@'on} boxplot (@var{data}, @var{option_1}, @var{option_2}, @dots{})
@deffnx {Funci@'on} boxplot_description (@dots{})

Dibuja diagramas de cajas (box-and-whishker). El argumento @var{data} puede
ser una lista, lo cual no es de gran inter@'es, puesto que estos gr@'aficos 
se utilizan principalmente para comparar distintas muestras, o una matriz,
de manera que sea posible comparar dos o m@'as componentes de una muestra
multivariante. Tambi@'en se permite que @var{data} sea una lista de muestras
con posibles tama@~nos diferentes; de hecho, esta es la @'unica funci@'on
del paquete @code{descriptive} que admite esta estructura de datos.

Opciones disponibles:

@itemize @bullet

@item
@var{box_width} (valor por defecto, @code{3/4}): ancho relativo de las cajas.
This  value must be in the range @code{[0,1]}.

@item
@var{box_orientation} (valor por defecto, @code{vertical}): valores posibles: @code{vertical}
y @code{horizontal}.

@item
Todas las opciones globales de @code{draw}, excepto @code{points_joined}, 
@code{point_size}, @code{point_type}, @code{xtics}, @code{ytics}, @code{xrange}
y @code{yrange}, que se asignan internamente por @code{boxplot}. Si es necesario 
que el usuario le d@'e sus propios valores a estas opciones, o quiere construir 
una escena m@'as compleja, debe hacer uso de @code{boxplot_description}.

@item
Las siguientes opciones locales de @code{draw}: @code{key}, @code{color}, 
y @code{line_width}.

@end itemize

La funci@'on @code{boxplot_description} crea un objeto gr@'afico
@'util para formar escenas complejas, junto con otros objetos gr@'aficos.
Se dispone tambi@'en de la funci@'on @code{wxboxplot} para crear
histogramas incorporados en los interfaces wxMaxima y iMaxima.

Ejemplos:

Diagrama de cajas de una muestra multivariante.

@c ===beg===
@c load (descriptive)$
@c s2 : read_matrix(file_search("wind.data"))$
@c boxplot(s2,
@c   box_width  = 0.2,
@c   title      = "Windspeed in knots",
@c   xlabel     = "Stations",
@c   color      = red,
@c   line_width = 2)$
@c ===end===
@example
(%i1) load (descriptive)$
(%i2) s2 : read_matrix(file_search("wind.data"))$
@group
(%i3) boxplot(s2,
        box_width  = 0.2,
        title      = "Windspeed in knots",
        xlabel     = "Stations",
        color      = red,
        line_width = 2)$
@end group
@end example

Diagrama de cajas de tres muestras de tama@~nos diferentes.

@c ===beg===
@c load (descriptive)$
@c A :
@c  [[6, 4, 6, 2, 4, 8, 6, 4, 6, 4, 3, 2],
@c   [8, 10, 7, 9, 12, 8, 10],
@c   [16, 13, 17, 12, 11, 18, 13, 18, 14, 12]]$
@c boxplot (A, box_orientation = horizontal)$
@c ===end===
@example
(%i1) load (descriptive)$
@group
(%i2) A :
       [[6, 4, 6, 2, 4, 8, 6, 4, 6, 4, 3, 2],
        [8, 10, 7, 9, 12, 8, 10],
        [16, 13, 17, 12, 11, 18, 13, 18, 14, 12]]$
@end group
(%i3) boxplot (A, box_orientation = horizontal)$
@end example

@end deffn



@deffn {Funci@'on} histogram (@var{list})
@deffnx {Funci@'on} histogram (@var{list}, @var{option_1}, @var{option_2}, @dots{})
@deffnx {Funci@'on} histogram (@var{one_column_matrix})
@deffnx {Funci@'on} histogram (@var{one_column_matrix}, @var{option_1}, @var{option_2}, @dots{})
@deffnx {Funci@'on} histogram (@var{one_row_matrix})
@deffnx {Funci@'on} histogram (@var{one_row_matrix}, @var{option_1}, @var{option_2}, @dots{})
@deffnx {Funci@'on} histogram_description (@dots{})

Dibuja un histograma a partir de una muestra continua. Los datos
muestrales deben darse en forma de lista de n@'umeros o como una
matriz unidimensional.

Opciones dispponibles:

@itemize @bullet

@item
@var{nclasses} (valor por defecto, @code{10}): n@'umero de clases del histograma, o
una lista indicando los l@'{@dotless{i}}mites de las clases y su n@'umero,
o solamente los l@'{@dotless{i}}mites.

@item
@var{frequency} (valor por defecto, @code{absolute}): indica la escala de las ordenadas.
Valores admitidos son:  @code{absolute}, @code{relative}, @code{percent} y @code{density}.
Con @code{density}, el histograma adquiere un @'area total igual a uno.

@item
@var{htics} (valor por defecto, @code{auto}): formato para las marcas sobre el
eje de las abscisas. Valores admitidos son: @code{auto}, @code{endpoints}, 
@code{intervals} o una lista de etiquetas. 

@item
Todas las opciones globales de @code{draw}, excepto @code{xrange}, @code{yrange}
y @code{xtics}, que son asignadas internamente por @code{histogram}.
Si es necesario que el usuario le d@'e sus propios valores a estas opciones,
debe hacer uso de @code{histogram_description}. V@'ease el ejemplo m@'as abajo.

@item
Las siguientes opciones locales de @code{draw}: @code{key}, @code{color}, 
@code{fill_color}, @code{fill_density} y @code{line_width}. V@'ease tambi@'en
@code{bars}.

@end itemize

La funci@'on @code{histogram_description} crea un objeto gr@'afico
@'util para formar escenas complejas, junto con otros objetos gr@'aficos.
Se dispone tambi@'en de la funci@'on @code{wxhistogram} para crear
histogramas incorporados en los interfaces wxMaxima y iMaxima.

Ejemplos:

Un histograma con seis clases:

@c ===beg===
@c load (descriptive)$
@c s1 : read_list (file_search ("pidigits.data"))$
@c histogram (
@c      s1,
@c      nclasses     = 8,
@c      title        = "pi digits",
@c      xlabel       = "digits",
@c      ylabel       = "Absolute frequency",
@c      fill_color   = grey,
@c      fill_density = 0.6)$
@c ===end===
@example
(%i1) load (descriptive)$
(%i2) s1 : read_list (file_search ("pidigits.data"))$
(%i3) histogram (
           s1,
           nclasses     = 8,
           title        = "pi digits",
           xlabel       = "digits",
           ylabel       = "Absolute frequency",
           fill_color   = grey,
           fill_density = 0.6)$
@end example

Ajustando los l@'{@dotless{i}}mites del histograma a -2 y 12,
con 3 clases. Tambi@'en se establece un formato predefinido a
las marcas del eje de abscisas:

@example
(%i1) load (descriptive)$
(%i2) s1 : read_list (file_search ("pidigits.data"))$
(%i3) histogram (
           s1,
           nclasses     = [-2,12,3],
           htics        = ["A", "B", "C"],
           terminal     = png,
           fill_color   = "#23afa0",
           fill_density = 0.6)$
@end example

Se hace uso de @code{histogram_description} para ajustar
la opci@'on @code{xrange} y a@~nadir una curva
expl@'{@dotless{i}}cita a la escena:

@example
(%i1) load (descriptive)$
(%i2) ( load("distrib"),
        m: 14, s: 2,
        s2: random_normal(m, s, 1000) ) $
(%i3) draw2d(
        grid   = true,
        xrange = [5, 25],
        histogram_description(
          s2,
          nclasses     = 9,
          frequency    = density,
          fill_density = 0.5),
        explicit(pdf_normal(x,m,s), x, m - 3*s, m + 3* s))$
@end example

@end deffn




@deffn {Funci@'on} piechart (@var{list})
@deffnx {Funci@'on} piechart (@var{list}, @var{option_1}, @var{option_2}, @dots{})
@deffnx {Funci@'on} piechart (@var{one_column_matrix})
@deffnx {Funci@'on} piechart (@var{one_column_matrix}, @var{option_1}, @var{option_2}, @dots{})
@deffnx {Funci@'on} piechart (@var{one_row_matrix})
@deffnx {Funci@'on} piechart (@var{one_row_matrix}, @var{option_1}, @var{option_2}, @dots{})
@deffnx {Funci@'on} piechart_description (@dots{})

Similar a @code{barsplot}, pero dibuja sectores en lugar de rect@'angulos.

Opciones disponibles:


@itemize @bullet

@item
@var{sector_colors} (valor por defecto, @code{[]}): una lista de colores para los
sectores. Cuando el n@'umero de sectores sea mayor que el de colores especificados, 
los colores adicionales necesarios se seleccionan aleatoriamente. V@'ease @code{color} para m@'as
informaci@'on.

@item
@var{pie_center} (valor por defecto, @code{[0,0]}): centro del diagrama

@item
@var{pie_radius} (valor por defecto, @code{1}): radio del diagrama.

@item
Todas las opciones globales de @code{draw}, excepto @code{key}, que se asigna
internamente por @code{piechart}. Si es necesario que el usuario le d@'e su propio
valor a esta opci@'on, o quiere construir una escena m@'as compleja,
debe hacer uso de @code{piechart_description}.

@item
Las siguientes opciones locales de @code{draw}: @code{key}, @code{color}, 
@code{fill_density} y @code{line_width}. V@'ease tambi@'en
@code{bars}.

@end itemize

La funci@'on @code{piechart_description} crea un objeto gr@'afico
@'util para formar escenas complejas, junto con otros objetos gr@'aficos.
Se dispone tambi@'en de la funci@'on @code{wxpiechart} para crear
histogramas incorporados en los interfaces wxMaxima y iMaxima.

Ejemplo:

@c ===beg===
@c load (descriptive)$
@c s1 : read_list (file_search ("pidigits.data"))$
@c piechart(
@c   s1,
@c   xrange = [-1.1, 1.3],
@c   yrange = [-1.1, 1.1],
@c   title  = "Digit frequencies in pi")$
@c ===end===
@example
(%i1) load (descriptive)$
(%i2) s1 : read_list (file_search ("pidigits.data"))$
(%i3) piechart(
        s1,
        xrange  = [-1.1, 1.3],
        yrange  = [-1.1, 1.1],
        title   = "Digit frequencies in pi")$
@end example

V@'ease tambi@'en la funci@'on @code{barsplot}.
@end deffn



@deffn {Funci@'on} scatterplot (@var{list})
@deffnx {Funci@'on} scatterplot (@var{list}, @var{option_1}, @var{option_2}, @dots{})
@deffnx {Funci@'on} scatterplot (@var{matrix})
@deffnx {Funci@'on} scatterplot (@var{matrix}, @var{option_1}, @var{option_2}, @dots{})
@deffnx {Funci@'on} scatterplot_description (@dots{})

Dibuja diagramas de dispersi@'on, tanto de muestras univariantes (@var{list})
como multivariantes (@var{matrix}). 

Las opciones disponibles son las mismas que admite @code{histogram}.

La funci@'on @code{scatterplot_description} crea un objeto gr@'afico
@'util para formar escenas complejas, junto con otros objetos gr@'aficos.
Se dispone tambi@'en de la funci@'on @code{wxscatterplot} para crear
histogramas incorporados en los interfaces wxMaxima y iMaxima.

Ejemplos:

Diagrama de dispersi@'on univariante a partir de una muestra normal
simulada. 

@c ===beg===
@c load (descriptive)$
@c load (distrib)$
@c scatterplot(
@c   random_normal(0,1,200),
@c   xaxis      = true,
@c   point_size = 2,
@c   dimensions = [600,150])$
@c ===end===
@example
(%i1) load (descriptive)$
(%i2) load (distrib)$
@group
(%i3) scatterplot(
        random_normal(0,1,200),
        xaxis      = true,
        point_size = 2,
        dimensions = [600,150])$
@end group
@end example

Diagrama de dispersi@'on bidimensional.

@c ===beg===
@c load (descriptive)$
@c s2 : read_matrix (file_search ("wind.data"))$
@c scatterplot(
@c  submatrix(s2, 1,2,3),
@c  title      = "Data from stations #4 and #5",
@c  point_type = diamant,
@c  point_size = 2,
@c  color      = blue)$
@c ===end===
@example
(%i1) load (descriptive)$
(%i2) s2 : read_matrix (file_search ("wind.data"))$
@group
(%i3) scatterplot(
       submatrix(s2, 1,2,3),
       title      = "Data from stations #4 and #5",
       point_type = diamant,
       point_size = 2,
       color      = blue)$
@end group
@end example

Diagrama de dispersi@'on tridimensional.

@c ===beg===
@c load (descriptive)$
@c s2 : read_matrix (file_search ("wind.data"))$
@c scatterplot(submatrix (s2, 1,2), nclasses=4)$
@c ===end===
@example
(%i1) load (descriptive)$
(%i2) s2 : read_matrix (file_search ("wind.data"))$
(%i3) scatterplot(submatrix (s2, 1,2), nclasses=4)$
@end example

Diagrama de dispersi@'on de cinco dimensiones, con histogramas de
cinco classes.

@c ===beg===
@c load (descriptive)$
@c s2 : read_matrix (file_search ("wind.data"))$
@c scatterplot(
@c   s2,
@c   nclasses     = 5,
@c   frequency    = relative,
@c   fill_color   = blue,
@c   fill_density = 0.3,
@c   xtics        = 5)$
@c ===end===
@example
(%i1) load (descriptive)$
(%i2) s2 : read_matrix (file_search ("wind.data"))$
@group
(%i3) scatterplot(
        s2,
        nclasses     = 5,
        frequency    = relative,
        fill_color   = blue,
        fill_density = 0.3,
        xtics        = 5)$
@end group
@end example

Para dibujar puntos aislados o unidos por segmentos, tanto en dos
como en tres dimensiones, v@'ease @code{points}.
V@'ease tambi@'en @code{histogram}.
@end deffn



@deffn {Funci@'on} starplot (@var{data1}, @var{data2}, @dots{}, @var{option_1}, @var{option_2}, @dots{})
@deffnx {Funci@'on} starplot_description (@dots{})

Dibuja diagramas de estrellas para variables estad@'{@dotless{i}}sticas
discretas, tanto para una como para m@'as muestras.

@var{data} puede ser una lista de resultados provenientes de una muestra
o una matriz de @var{m} filas y @var{n} columnas, representando @var{n} 
muestras de tama@~no @var{m} cada una.

Las opciones disponibles son:

@itemize @bullet

@item
@var{stars_colors} (valor por defecto, @code{[]}): una lista de colores para muestras
m@'ultiples. Cuando haya m@'as muestras que colores especificados, los colores que faltan
se eligen aleatoriamente. V@'ease @code{color} para m@'as informaci@'on.

@item
@var{frequency} (valor por defecto, @code{absolute}): indica la escala de los radios.
Valores admitidos son:  @code{absolute} y @code{relative}.

@item
@var{ordering} (valor por defecto, @code{orderlessp}): los valores admitidos para esta 
opci@'on son: @code{orderlessp} y @code{ordergreatp}, indicando c@'omo se deben ordenar
los resultados muestrales sobre el eje-@var{x}.


@item
@var{sample_keys} (valor por defecto, @code{[]}): es una lista de cadenas de 
texto a usar como leyendas. Cuando la lista tenga una longitud diferente de cero o 
del n@'umero de muestras, se devolver@'a un mensaje de error.

@item
@var{star_center} (valor por defecto, @code{[0,0]}): centro del diagrama.

@item
@var{star_radius} (valor por defecto, @code{1}): radio del diagrama.

@item
Todas las opciones globales de @code{draw}, excepto @code{points_joined}, @code{point_type}, 
and @code{key}, que se asignan internamente por @code{starplot}. Si es necesario que el 
usuario les d@'e sus propios valores a estas opciones, o quiere construir una escena m@'as compleja,
debe hacer uso de @code{starplot_description}.

@item
La siguiente opci@'on local de @code{draw}: @code{line_width}.

@end itemize

La funci@'on @code{starplot_description} crea un objeto gr@'afico
@'util para formar escenas complejas, junto con otros objetos gr@'aficos.
Se dispone tambi@'en de la funci@'on @code{wxstarplot} para crear
histogramas incorporados en los interfaces wxMaxima y iMaxima.

Ejemplo:

Gr@'afico basado en frecuencias absolutas. La localizaci@'on y
el radios lo define el usuario.

@example
(%i1) load (descriptive)$
(%i2) l1: makelist(random(10),k,1,50)$
(%i3) l2: makelist(random(10),k,1,200)$
@group
(%i4) starplot(
        l1, l2,
        stars_colors = [blue,red],
        sample_keys = ["1st sample", "2nd sample"],
        star_center = [1,2],
        star_radius = 4,
        proportional_axes = xy,
        line_width = 2 ) $ 
@end group
@end example

@end deffn



@deffn {Funci@'on} stemplot (@var{m})
@deffnx {Funci@'on} stemplot (@var{m}, @var{option})

Dibuja diagrama de tallos y hojas.

La @'unica opci@'on disponible es:

@itemize @bullet

@item
@var{leaf_unit} (valor por defecto, @code{1}): indica la unidad de las hojas; debe ser
una potencia de 10.

@end itemize

Ejemplo:

@example
(%i1) load (descriptive)$
(%i2) load(distrib)$
@group
(%i3) stemplot(
        random_normal(15, 6, 100),
        leaf_unit = 0.1);
-5|4
 0|37
 1|7
 3|6
 4|4
 5|4
 6|57
 7|0149
 8|3
 9|1334588
10|07888
11|01144467789
12|12566889
13|24778
14|047
15|223458
16|4
17|11557
18|000247
19|4467799
20|00
21|1
22|2335
23|01457
24|12356
25|455
27|79
key: 6|3 =  6.3
(%o3)                  done
@end group
@end example

@end deffn
