@c English version: 2011-06-11
@menu
* Introducci@'on a drawdf::
* Funciones y variables para drawdf::
@end menu

@node Introducci@'on a drawdf, Funciones y variables para drawdf, drawdf, drawdf
@section Introducci@'on a drawdf

La funci@'on @code{drawdf} dibuja el campo de direcciones de una ecuaci@'on
diferencial ordinaria de primer orden (EDO) o de un sistema de dos ecuaciones
aut@'onomas de primer orden.

Puesto que @code{drawdf} es un paquete adicional, es necesario cargarlo en
memoria ejecutando previamente la instrucci@'on @code{load(drawdf)}.
@code{drawdf} utiliza el paquete @code{draw}, que necesita como 
m@'{@dotless{i}}nimo la versi@'on 4.2 de Gnuplot.

Para dibujar el campo de direcciones de una EDO, @'esta debe escribirse de la
forma
@ifnottex
@example
       dy
       -- = F(x,y)
       dx
@end example
@end ifnottex
@tex
$${{dy}\over{dx}} = F(x,y)$$
@end tex

y ser la funci@'on @var{F} la que se pase a @code{drawdf} como argumento.
Si las variables independiente y dependiente no son @var{x} e @var{y},
como en la ecuaci@'on anterior, entonces deben nombrarse de forma
expl@'{@dotless{i}}cita en una lista que se pasar@'a a @code{drawdf}
(ver ejemplos m@'as abajo).

Para dibujar el campo de direcciones de un conjunto de dos EDOs aut@'onomas,
deben escribirse de la forma
@ifnottex
@example
       dx             dy
       -- = G(x,y)    -- = F(x,y) 
       dt             dt
@end example
@end ifnottex
@tex
$${{dx}\over{dt}} = G(x,y) \qquad {{dy}\over{dt}} = F(x,y)$$
@end tex

y ser@'a una lista con las dos funciones @var{G} y @var{F} la que
se pase a @code{drawdf} como argumento. El orden de las funciones es
importante; la primera ser@'a la derivada de la abscisa respecto del tiempo
y la segunda la derivada de la ordenada respecto del tiempo.
Si las variables no son las @var{x} e @var{y} habituales, el segundo
argumento que se pase a la funci@'on @code{drawdf} ser@'a una lista
con los nombres de ambas variables, primero la correspondiente a las
abscisas, seguida de la asociada a las ordenadas.

Si s@'olo se trata de una EDO, @code{drawdf} admitir@'a por defecto
que @code{x=t} y @code{G(x,y)=1}, transformanado la ecuaci@'on no
aut@'onoma en un sistema de dos ecuaciones aut@'onomas.




@node Funciones y variables para drawdf,  , Introducci@'on a drawdf, drawdf
@section Funciones y variables para drawdf

@subsection Funciones

@deffn {Funci@'on} drawdf (@var{dydx}, ...options and objects...)
@deffnx {Funci@'on} drawdf (@var{dvdu}, @code{[}@var{u},@var{v}@code{]}, ...opciones y objetos...)
@deffnx {Funci@'on} drawdf (@var{dvdu}, @code{[}@var{u},@var{umin},@var{umax}@code{]}, @code{[}@var{v},@var{vmin},@var{vmax}@code{]}, ...opciones y objetos...)
@deffnx {Funci@'on} drawdf (@code{[}@var{dxdt},@var{dydt}@code{]}, ...opciones y objetos...)
@deffnx {Funci@'on} drawdf (@code{[}@var{dudt},@var{dvdt}@code{]}, @code{[}@var{u},@var{v}@code{]}, ...opciones y objetos...)
@deffnx {Funci@'on} drawdf (@code{[}@var{dudt},@var{dvdt}@code{]}, @code{[}@var{u},@var{umin},@var{umax}@code{]}, @code{[}@var{v},@var{vmin},@var{vmax}@code{]}, ...opciones y objetos...)

La funci@'on @code{drawdf} dibuja un campo de direcciones 2D, incluyendo
opcionalmente curvas asociadas a soluciones particulares, as@'{@dotless{i}}
como otros objeto gr@'aficos del paquete @code{draw}.

El primer argumento especifica la(s) derivada(s), el cual debe estar
formado por una expresi@'on o una lista de dos expresiones. @var{dydx}, @var{dxdt} y
@var{dydt} son expresiones que dependen de @var{x} y @var{y}.
@var{dvdu}, @var{dudt} y @var{dvdt} son expresiones que dependen de
@var{u} y @var{v}.

Si las variables independiente y dependiente no son @var{x} e @var{y},
sus nombres deben especificarse a continuaci@'on de las derivadas,
bien como una lista de dos nombres,
@code{[}@var{u},@var{v}@code{]}, o como dos listas de la forma
@code{[}@var{u},@var{umin},@var{umax}@code{]} y
@code{[}@var{v},@var{vmin},@var{vmax}@code{]}.

El resto de argumentos son @i{opciones gr@'aficas}, @i{objetos gr@'aficos},
o listas conteniendo elementos de ambos tipos. El conjunto de opciones y
objetos gr@'aficos soportados por @code{drawdf} incluye los soportados
por @code{draw2d} y @code{gr2d} del paquete @code{draw}.

Los argumentos se iterpretan secuencialmente: las @i{opciones gr@'aficas}
afectan a todos los @i{objetos gr@'aficos} que le siguen. Adem@'as,
los @i{objetos gr@'aficos} se dibujan en el orden en el que se
especifican, pudiendo tapar otros gr@'aficos dibujados con anterioridad.
Algunas @i{opciones gr@'aficas} afectan al aspecto general de la escena.

Los nuevos @i{objetos gr@'aficos} que se definen en @code{drawdf} son:
@code{solns_at}, @code{points_at}, @code{saddles_at}, @code{soln_at},
@code{point_at} y @code{saddle_at}.

Las nuevas @i{opciones gr@'aficas} que se definen en @code{drawdf} son:
@code{field_degree}, @code{soln_arrows}, @code{field_arrows},
@code{field_grid}, @code{field_color}, @code{show_field},
@code{tstep}, @code{nsteps}, @code{duration}, @code{direction},
@code{field_tstep}, @code{field_nsteps} y @code{field_duration}.

@i{Objetos gr@'aficos} que se heredan del paquete @code{draw} incluyen:
@code{explicit}, @code{implicit}, @code{parametric},
@code{polygon}, @code{points}, @code{vector}, @code{label} y cualesquiera
otros soportados por @code{draw2d} y @code{gr2d}.

@i{Opciones gr@'aficas} que se heredan del paquete @code{draw} incluyen:
@code{points_joined}, @code{color},
@code{point_type}, @code{point_size}, @code{line_width},
@code{line_type}, @code{key}, @code{title}, @code{xlabel},
@code{ylabel}, @code{user_preamble}, @code{terminal},
@code{dimensions}, @code{file_name} y cualesquiera
otros soportados por @code{draw2d} y @code{gr2d}.

V@'ease tambi@'en @code{draw2d}. 

Los usuarios de wxMaxima y Imaxima pueden hacer uso tambi@'en @code{wxdrawdf},
que es id@'entica a @code{drawdf}, pero que inserta el gr@'afico en el 
propio documento utilizando la funci@'on @code{wxdraw}.

Para hacer uso de esta funci@'on, ejec@'utese primero @code{load(drawdf)}.

Ejemplos:

@example
(%i1) load(drawdf)$
(%i2) drawdf(exp(-x)+y)$        /* default vars: x,y */
(%i3) drawdf(exp(-t)+y, [t,y])$ /* default range: [-10,10] */
(%i4) drawdf([y,-9*sin(x)-y/5], [x,1,5], [y,-2,2])$
@end example

A efectos de compatibilidad, @code{drawdf} acepta la mayor
parte de par@'ametros soportados por @code{plotdf}.

@example
(%i5) drawdf(2*cos(t)-1+y, [t,y], [t,-5,10], [y,-4,9],
             [trajectory_at,0,0])$
@end example

@code{soln_at} y @code{solns_at} dibujan soluciones particulares
que pasen por los puntos especificados, utilizando el integrador
num@'erico de Runge Kutta de cuarto orden.

@example
(%i6) drawdf(2*cos(t)-1+y, [t,-5,10], [y,-4,9],
             solns_at([0,0.1],[0,-0.1]),
             color=blue, soln_at(0,0))$
@end example

@code{field_degree=2} hace que el campo se componga de @i{splines}
cuadr@'aticos basados en las derivadas de primer y segundo orden en
cada punto de la malla. 
@code{field_grid=[}@var{COLS},@var{ROWS}@code{]} 
especifica el n@'umero de columnas y filas a utilizar en la malla.

@example
(%i7) drawdf(2*cos(t)-1+y, [t,-5,10], [y,-4,9],
             field_degree=2, field_grid=[20,15],
             solns_at([0,0.1],[0,-0.1]),
             color=blue, soln_at(0,0))$
@end example

@code{soln_arrows=true} a@~nade flechas a las soluciones particulares y,
por defecto, las borra. Tambi@'en cambia los colores por defecto para 
destacar las curvas de las soluciones particulares.

@example
(%i8) drawdf(2*cos(t)-1+y, [t,-5,10], [y,-4,9],
             soln_arrows=true,
             solns_at([0,0.1],[0,-0.1],[0,0]))$
@end example

@code{duration=40} especifica el intervalo temporal de la integraci@'on
num@'erica, cuyo valor por defecto es 10. La integraci@'on se detendr@'a
autom@'aticamente si la soluci@'on se aparta mucho de la regi@'on a dibujar,
o si la derivada se vuelve compleja o infinita. Aqu@'{@dotless{i}} tambi@'en
hacemos @code{field_degree=2} para dibujar @i{splines} cuadr@'aticos. Las
ecuaciones de este ejemplo modelizan un sistema depredador-presa.

@example
(%i9) drawdf([x*(1-x-y), y*(3/4-y-x/2)], [x,0,1.1], [y,0,1],
             field_degree=2, duration=40,
             soln_arrows=true, point_at(1/2,1/2),
             solns_at([0.1,0.2], [0.2,0.1], [1,0.8], [0.8,1],
                      [0.1,0.1], [0.6,0.05], [0.05,0.4],
                      [1,0.01], [0.01,0.75]))$
@end example

@code{field_degree='solns} hace que el campo se componga de muchas
peque@~nas soluciones calculadas a partir del Runge Kutta
de cuarto orden.

@example
(%i10) drawdf([x*(1-x-y), y*(3/4-y-x/2)], [x,0,1.1], [y,0,1],
              field_degree='solns, duration=40,
              soln_arrows=true, point_at(1/2,1/2),
              solns_at([0.1,0.2], [0.2,0.1], [1,0.8],
                       [0.8,1], [0.1,0.1], [0.6,0.05],
                       [0.05,0.4], [1,0.01], [0.01,0.75]))$
@end example

@code{saddles_at} trata de linearizar autom@'aticamente la ecuaci@'on
en cada punto de silla y dibujar la soluci@'on num@'erica correspondiente
a cada vector propio, incluyendo las separatrices. @code{tstep=0.05} establece
el salto temporal m@'aximo para el integrador num@'erico, cuyo valor por
defecto es 0.1. Las siguientes ecuaciones modelizan un p@'endulo amortiguado.

@example
(%i11) drawdf([y,-9*sin(x)-y/5], tstep=0.05,
              soln_arrows=true, point_size=0.5,
              points_at([0,0], [2*%pi,0], [-2*%pi,0]),
              field_degree='solns,
              saddles_at([%pi,0], [-%pi,0]))$
@end example

@code{show_field=false} elimina el campo completamente.

@example
(%i12) drawdf([y,-9*sin(x)-y/5], tstep=0.05,
              show_field=false, soln_arrows=true,
              point_size=0.5,
              points_at([0,0], [2*%pi,0], [-2*%pi,0]),
              saddles_at([3*%pi,0], [-3*%pi,0],
                         [%pi,0], [-%pi,0]))$
@end example

@code{drawdf} pasa todos los par@'ametros que no reconoce a @code{draw2d}
o @code{gr2d}, permitiendo combinar la potencia del paquete @code{draw}
con @code{drawdf}.

@example
(%i13) drawdf(x^2+y^2, [x,-2,2], [y,-2,2], field_color=gray,
              key="soln 1", color=black, soln_at(0,0),
              key="soln 2", color=red, soln_at(0,1),
              key="isocline", color=green, line_width=2,
              nticks=100, parametric(cos(t),sin(t),t,0,2*%pi))$
@end example

@code{drawdf} acepta listas anidadas de opciones y objetos gr@'aficos,
permitiendo el uso de @code{makelist} y otras funciones de forma m@'as
flexible para generar gr@'aficos.

@example
(%i14) colors : ['red,'blue,'purple,'orange,'green]$
(%i15) drawdf([x-x*y/2, (x*y - 3*y)/4],
              [x,2.5,3.5], [y,1.5,2.5],
              field_color = gray,
              makelist([ key   = concat("soln",k),
                         color = colors[k],
                         soln_at(3, 2 + k/20) ],
                       k,1,5))$
@end example

@end deffn

